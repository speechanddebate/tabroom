<%args>
	$round
	$tiebreak_set => undef
	$simple       => undef
	$delimiter    => undef
	$round_name   => undef
	$entries_ref  => undef
</%args>
<%init>

	unless ($round && $round->id) {
		return;
	}

	my $event = $round->event;
	my $type = $event->type;

	my $points = $event->setting("point_increments");
	my $sprintf;

	if ($points eq "fourths") {
		$sprintf = '%.2f';
	} elsif ( $points eq "half" || $points eq "tenths") {
		$sprintf = '%.1f';
	} else {
		$sprintf = '%3d';
	}

	unless ($round->tiebreak_set) {
		my $err = "That round does not have tiebreakers set.  ";
		$err .= "Please establish tiebreakers for all rounds";

		$m->redirect("/setup/schedule/event.mhtml?event_id=".$event->id."&err=$err");
	}

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("
		select score.id, score.tag, score.value, score.student,
			ballot.entry, ballot.judge, ballot.tv,
			panel.bye, ballot.bye, ballot.forfeit, ballot.chair,
			round.id, round.name
		from (ballot, panel, round)
		left join score on score.ballot = ballot.id
			and score.tag in ('winloss', 'rank', 'point', 'refute')
		where ballot.panel = panel.id
		and panel.round = round.id
		and round.name <= ?
		and round.event =  ?
		and not exists (
			select round_setting.id
				from round_setting where round_setting.tag = 'ignore_results'
				and round_setting.round = round.id
			)
		order by round.name, ballot.entry, ballot.chair, ballot.judge, score.student
	");

	$round_name = int($round_name);
	$round_name = $round->name unless $round_name;
	$sth->execute($round_name, $event->id);

	my %rounds;
	my %entries;
	my %done;

	while (
		my (
			$id, $tag, $value, $student,
			$entry_id, $judge_id, $tv,
			$pbye, $bye, $fft, $chair,
			$round_id, $roundname
		) = $sth->fetchrow_array()
	) {

		next if $done{$id}++;

		unless ($tag) {

			if ($pbye || $bye) {
				$entries{$entry_id}{$round_id}{"full"} = "BYE ";
			} elsif ($fft) {
				$entries{$entry_id}{$round_id}{"full"} = "FORFEIT ";
			}

			next;
		}

		if ($tag eq "point") {
			$value = sprintf($sprintf, $value);
		}

		$entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag} .= ","
			if $entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag};

		$rounds{$round_id} = $roundname;

		if ($tag eq "winloss") {
			$entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag} .= " W " if $value;
			$entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag} .= " L " unless $value;
		} else {
			$entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag} .= " "
				if $entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag};
			$entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag} .= $value;
			$entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag} .= '*'
				if $type eq "congress" && $chair;

			if ($tv && $tag eq "rank") {
				$entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag} .= '/VIOL'
			}
		}

		$entries{$entry_id}{$round_id}{"total"}{$tag} = 0
			unless $entries{$entry_id}{$round_id}{"total"}{$tag};

		$entries{$entry_id}{$round_id}{"total"}{$tag} += $value;
		$entries{$entry_id}{$round_id}{"total"}{"point"} += $value
			if $tag eq "refute";

		$entries{$entry_id}{$round_id}{"chair"} += $value if $chair;
		$entries{$entry_id}{$round_id}{"students"}{$student}++ if $student;

	}

	if ($entries_ref) {
		foreach my $entry_id (keys %entries) {
			foreach my $round_id (keys %{$entries{$entry_id}}) {
				if ($entries_ref->{"used_composite"}{$entry_id}{$round_id}) {
					$entries{$entry_id}{$round_id}{"composite"}
						= $entries_ref->{"used_composite"}{$entry_id}{$round_id};
				}
			}
		}
	}

	my %entry_ballots;

	my @scores;

	if ($type eq 'speech' || $type eq "congress") {
		@scores = ("rank", "point");
	} elsif ($type eq "wudc") {
		@scores = ("winloss", "point", "refute", "rank");
	} else {
		@scores = ("winloss", "point", "rank");
	}

	foreach my $entry_id (keys %entries) {

		foreach my $round_id (
			sort {$rounds{$a} <=> $rounds{$b}}
			keys %{$entries{$entry_id}}
		) {

			$entry_ballots{$entry_id} .= "\n";
			$entry_ballots{$entry_id} .= "R".$rounds{$round_id};

			foreach (length($rounds{$round_id}) .. 2) {
				$entry_ballots{$entry_id} .= " ";
			}

			if ($entries{$entry_id}{$round_id}{"full"}) {
				$entry_ballots{$entry_id} .=  $entries{$entry_id}{$round_id}{"full"}." ";
			}

			foreach my $judge_id (sort keys %{$entries{$entry_id}{$round_id}{'judges'}}) {
				foreach my $tag (@scores) {
					if ($entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag}) {
						$entry_ballots{$entry_id} .= " ".$entries{$entry_id}{$round_id}{"judges"}{$judge_id}{$tag};
					}
				}
			}

			if (defined $entries{$entry_id}{$round_id}{"total"}{"winloss"}) {
				if (scalar (keys %{$entries{$entry_id}{$round_id}{'judges'}}) > 1) {

					my $judges = scalar (keys %{$entries{$entry_id}{$round_id}{"judges"}});
					my $wins = $entries{$entry_id}{$round_id}{"total"}{"winloss"};
					my $remainder = $judges - $wins;

					$entry_ballots{$entry_id} .= " (";
					$entry_ballots{$entry_id} .= $wins."-";
					$entry_ballots{$entry_id} .= $remainder;
					$entry_ballots{$entry_id} .= ")";
				}
			}

			if (
				scalar (keys %{$entries{$entry_id}{$round_id}{"students"}}) > 1
				|| scalar (keys %{$entries{$entry_id}{$round_id}{'judges'}}) > 1
			) {

				my $notfirst;

				foreach my $tag (@scores) {
					next if $tag eq "refute";
					next if $tag eq "winloss";
					next unless $entries{$entry_id}{$round_id}{"total"}{$tag};

					my $value = $entries{$entry_id}{$round_id}{"total"}{$tag};
					$value = sprintf($sprintf, $value) if $tag eq "point";

					if ($notfirst++) {
						$entry_ballots{$entry_id} .= ",";
					} else {
						$entry_ballots{$entry_id} .= " (";
					}

					$entry_ballots{$entry_id} .= $value;
				}

				$entry_ballots{$entry_id} .= ") " if $notfirst;
			}

#			if ($entries{$entry_id}{$round_id}{"composite"}) {
#				$entry_ballots{$entry_id} .= " Comp: ".$entries{$entry_id}{$round_id}{"composite"};
#			}
		}
	}

	return %entry_ballots;

</%init>
