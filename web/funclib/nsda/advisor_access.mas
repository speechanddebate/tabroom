<%args>
	$person
</%args>
<%init>

	unless ($person->nsda > 0) {
		return;
	}

	my $dbh = Tab::DBI->db_Main();

	# Check to make sure I have access to any schools I am the advisor for.

	my ($roles_ref, $roles_json) = $m->comp(
		"/funclib/nsda/api_client.mas",
		path => "/members/".$person->nsda."/roles"
	);

	my $already_sth = $dbh->prepare("
		select chapter.id
			from chapter
		where chapter.nsda = ?
		and NOT EXISTS (
			select permission.id
				from permission
			where permission.person = ?
				and permission.tag = 'chapter'
				and permission.chapter = chapter.id
		)
	");

	my $now = DateTime->now();
	my $string = DateTime::Format::MySQL->format_datetime($now);
	$string =~ s/[\D_]//g;

	if ($roles_ref && @{$roles_ref}) {

		foreach my $role (@{$roles_ref}) {

			next if $role->{role} ne "Advisor";
			my $time = $role->{end};

			if ($time) {
				$time =~ s/[\D_]//g;
				if ($time < $string) {
					next;
				}
			}

			$already_sth->execute($role->{school_id}, $person->id);
			my $chapters = $already_sth->fetchall_hash();

			if ($chapters && @{$chapters}) {
				foreach my $chapter (@{$chapters}) {

					my $perm = eval {
						return Tab::Permission->create({
							person  => $person->id,
							tag     => "chapter",
							chapter => $chapter->{id}
						});
					};

					if ($perm) {
						Tab::ChangeLog->create({
							tag         => "chapter",
							chapter     => $chapter->{id},
							person      => $person->id,
							created_at  => DateTime->now(),
							description => $person->email." given automatic access because they are an NSDA advisor to school ID ".$role->{school}." end date ".$role->{end},
						});
					}
				}
			}
		}
	}
</%init>
