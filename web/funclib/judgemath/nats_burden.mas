<%args>
	$category
	$school
</%args>
<%init>

	my %burdens;

    my $dbh = Tab::DBI->db_Main();

    my $sth = $dbh->prepare("
		select distinct entry.id,
			event.id as eventid,
			burden.value burden,
			nats_jpool.value jpool,
			jpool.name jpool_name,
			nats_screwy_burden.value nats_screwy_burden,
			hire.value as hire,
			hire_price.value as hire_price,
			njb.value, reject.value

			from (entry, event,
				event_setting burden,
				event_setting nats_jpool,
				jpool)

			left join event_setting nats_screwy_burden
				on nats_screwy_burden.event = event.id
				and nats_screwy_burden.tag  = 'nats_screwy_burden'

			left join jpool_setting hire
				on hire.jpool = nats_jpool.value
				and hire.tag = 'hire'

			left join jpool_setting hire_price
				on hire_price.jpool = nats_jpool.value
				and hire_price.tag = 'hire_price'

 	       left join event_setting njb
				on njb.event = event.id
				and njb.tag = 'no_judge_burden'

	        left join entry_setting reject
				on reject.entry = entry.id
				and reject.tag = 'rejected_by'

		where entry.school = ?
			and entry.unconfirmed = 0
			and entry.event = event.id
			and event.category = ?

			and burden.event = event.id
			and burden.tag = 'nats_judge_burden'

			and nats_jpool.event = event.id
			and nats_jpool.tag = 'nats_jpool'
			and jpool.id = nats_jpool.value

	");

	$sth->execute($school->id, $category->id);

	my %events;
	my $count_entries;

	while(

		my( $entry_id, $event_id, $burden, $jpool_id, $jpool_name,
			$nats_screwy_burden, $hire, $hire_price, $njb, $reject
		) = $sth->fetchrow_array()

	){

		next if $reject;
		next if $njb;

		$count_entries++;
		$events{$event_id}{"count"}++;
		$events{$event_id}{"jpool"} = $jpool_id;
		$events{$event_id}{"burden"} = $burden;
		$events{$event_id}{"screwy_burden"}++ if $nats_screwy_burden;
		$events{$event_id}{"hire"} = $hire;
		$events{$event_id}{"hire_price"} = $hire_price;

		$burdens{"jpools"}{$jpool_id}{"name"} = $jpool_name;
		$burdens{"jpools"}{$jpool_id}{"hire"} = $hire;
		$burdens{"jpools"}{$jpool_id}{"hire_price"} = $hire_price;
	}

	foreach my $event_id (keys %events) {

		my $burden;

		# This is some serious pissant bullshit right here.

		if ($events{$event_id}{"screwy_burden"}) {

			my $full = $events{$event_id}{"burden"};
			my $half = POSIX::ceil($full / 2);

			foreach my $count (1 .. $events{$event_id}{"count"}) {

				if ($count % 2) {
					#odds get full
					$burden += $full;
				} else {
					$burden += $half;
				}
			}

		} else {

			$burden += ($events{$event_id}{"count"} * $events{$event_id}{"burden"});

		}

		$burdens{$event_id}{"need"} += $burden;
		$burdens{$event_id}{"can_hire"} = $events{$event_id}{"hire"};

		$burdens{$events{$event_id}{"jpool"}} += $burden;
		$burdens{"total"} += $burden;

		$burdens{"hire"}{$events{$event_id}{"jpool"}}       = $events{$event_id}{"hire"};
		$burdens{"hire_price"}{$events{$event_id}{"jpool"}} = $events{$event_id}{"hire_price"};

	}

	my $min_burden = $category->setting("min_burden");
	my $max_burden = $category->setting("max_burden");

	if ($burdens{"total"} > 0
		&& $min_burden > 0
		&& $burdens{"total"} < $min_burden
	) {
		$burdens{"total"} = $min_burden;
	}

	if ( $max_burden
		&& $burdens{"total"} > $max_burden
	)  {
		$burdens{"total"} = $max_burden;
	}

	Tab::Judge->set_sql( other_judges => "
		select judge.* from judge
			where judge.school = ?
			and judge.category != ?
	");

	if ($category->setting('minimum_supplied')
		&& $count_entries > 0
		&& (not defined $school->setting("no_judge_warnings"))
	) {

		unless (Tab::Judge->search_other_judges($school->id, $category->id)) {
			$burdens{'total'} = $category->setting('minimum_supplied')
				if $burdens{'total'} < $category->setting('minimum_supplied');
		}
	}

	$burdens{'total'} = 0 unless $burdens{'total'};

	return %burdens;

</%init>
