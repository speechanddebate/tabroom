<%args>
	$panel    => undef
	$panel_id => undef
	$judge    => undef
	$judge_id => undef
	$chair    => 0
	$done     => 0
	$dbh      => undef
</%args>
<%init>

	unless ($dbh) {
		$dbh = Tab::DBI->db_Main();
	}

	return unless ($judge || $judge_id) && ($panel || $panel_id);

	$judge_id = $judge->id if $judge && (not defined $judge_id);
	$panel_id = $panel->id if $panel && (not defined $panel_id);

	# If I ever figure out what is sending a "4" as a chair flag I'm
	# going to murder it.

	$chair = 1 if $chair > 0;
	$chair = 0 if $chair < 1;

	my $sth = $dbh->prepare("
		select ballot.*
			from ballot
		where 1=1
			and ballot.panel = ?
			and ballot.entry IS NOT NULL
		order by ballot.judge DESC
	");

	$sth->execute($panel_id);
	my $ballots = $sth->fetchall_hash();

	my %taken;

	if ($judge && scalar @{$ballots} < 1) {
		Tab::Ballot->create({
			panel => $panel_id,
			judge => $judge,
		});
		return \1;
	}

	my $ballot_sth = $dbh->prepare("
		delete ballot.* from ballot where id = ?
	");

	foreach my $ballot (@{$ballots}) {

		if ($ballot->{judge} == $judge_id) {
			return;
		}

		if ($ballot->{judge} > 0) {
			unless ($taken{$ballot->{entry}}) {
				$taken{$ballot->{entry}} = $ballot;
			}
		} else {
			if ($taken{$ballot->{entry}}) {
				$ballot_sth->execute($ballot->{id});
			} else {
				$taken{$ballot->{entry}} = $ballot;
			}
		}
	}

	my $score_sth = $dbh->prepare("
		delete score.* from score where ballot = ?
	");

	my $copy_sth = $dbh->prepare("
		update ballot
			set judge_started = NULL, judge = ?, audit = ?, chair = ?
			where id = ?
	");

	my $new_ballot = $dbh->prepare("
		insert into ballot (side, speakerorder, seat, tv, audit, judge, panel, entry, chair)
			values (?, ?, ?, 0, ?, ?, ?, ?, ?)
	");

	foreach my $entry (keys %taken) {

		my $ballot = $taken{$entry};

		if ($ballot->{judge} > 0) {

			eval {
				$new_ballot->execute(
					$ballot->{side},
					$ballot->{speakerorder},
					$ballot->{seat},
					$done,
					$judge_id,
					$ballot->{panel},
					$ballot->{entry},
					$chair
				);
			};

			if ($@) {
				Tab::debuglog("Error on ballot copy for panel_judgeadd.mas: ".$@);
				return $@;
			}

		} else {

			eval {
				$copy_sth->execute($judge_id, $done, $chair, $ballot->{id});
			};

			if ($@) {
				Tab::debuglog("Error on ballot copy for panel_judgeadd.mas: ".$@);
				return $@;
			}

			$score_sth->execute($ballot->{id});
		}
	}

	my $empty_delete = $dbh->prepare("
		delete ballot.*
			from ballot
		where ballot.panel = ?
		and ballot.judge IS NULL
	");

	$empty_delete->execute();

	return;

</%init>
