<%args>
	$body
	$subject
	$from            => undef
	$from_string     => undef
	$real            => undef
	$to              => undef
	$array           => undef
	$accounts        => undef
	$cc_accounts     => undef
	$raw             => undef
	$cc              => undef
	$no_footer       => undef
	$attachment_name => undef
	$attachment_path => undef
	$override        => undef
</%args>
<%init>

	return unless ($from || $from_string);

	my $reply_to_string;

	if ($from_string eq "nsda") {
		if ($from) {
			$from_string = $from->first." ".$from->last." <info\@speechanddebate.org>";
		} else {
			$from_string = "NSDA Nationals Registration <info\@speechanddebate.org>";
		}
	}

	unless ($from_string) {
		$from_string = $from->first." ".$from->last." <info\@tabroom.com>" unless $real;
		$from_string = $from->first." ".$from->last." <".$from->email.">" if $real;
		$reply_to_string = $from->first." ".$from->last." <".$from->email.">";
	}

	my @send_array = @{$array} if $array;

	if ($to) {

		push @send_array, $to->email
		unless (
			$to->no_email == 1
			&& (not defined $override)
		);
	}

	if ($raw) {
		push @send_array, split(/\,/, $raw);
	}

	if ($accounts) {
		foreach my $account (@{$accounts}) {
			next if ($account->no_email && (not defined $override));
			push @send_array, $account->email;
		}
	}

	my %seen = ();
	@send_array = grep { ! $seen{$_} ++ } @send_array;

	return unless @send_array;

	my $hostname = $Tab::hostname;

	unless ($no_footer) {

		$body = $body."\n\n-----------------------------\n\n";

		$body = $body."You signed up for this email by registering on
		https://www.tabroom.com.\n\n";

		$body = $body."If you don't want to get emails from Tabroom.com, login
		to your account, click Profile on the top right, and check off \"No Emails\".
		If you do not have a Tabroom.com account, go to
		https://www.tabroom.com/unfollow\n\n";

	}

	Tab::log("send_email.mas: Sending tabroom email from ".$from_string." to @send_array");

	use HTML::FromText;

	my $html = text2html(
		$body,
		paras     => 1,
		metachars => 0
	);

	my $type = "text/html";

	$subject = "[TAB] ".$subject;

	my %done;

	while (@send_array) {

		my $counter;
		my $to_string;

		while ($counter < 6 && @send_array) {
			my $to = shift @send_array;
			next unless $to;
			next if $done{$to}++;

			$to_string .= "," if $to_string;
			$to_string .= $to;
			$counter++;
		}

		# creating new "base"-object for an email
		my $msg = MIME::Lite->new(
			From       => $from_string,
			To         => $from_string,
			Bcc        => $to_string,
			Subject    => $subject,
			"Reply-To" => $reply_to_string,
			Type       => $type,
			Data       => $html."\n\n"
		);

		if ($cc_accounts) {

			my $cc_field;

			foreach my $cc (@{$cc_accounts}) {
				next if $cc->no_email && (not defined $override);
				$cc_field .= "," if $cc_field;
				$cc_field .= $cc->first." ".$cc->last." <".$cc->email.">"
			}

			$msg->add(CC => $cc_field);

		} elsif (($raw && $cc) || ( $cc && $cc ne int($cc)) ) {

			$msg->add(CC => $cc);

		} elsif ($cc) {

			$msg->add(
				CC => $cc->first." ".$cc->last." <".$cc->email.">"
			)
			if ($cc->email
				&& $cc->no_email != 1
				&& (not defined $override)
			);
		}

		# Only works for pdfs. Should probably add a parameter so we can change attachment type.

		if ($attachment_name && $attachment_path) {
			$msg->attach (
				Type        => 'application/pdf',
				Path        => $attachment_path,
				Filename    => $attachment_name,
				Disposition => 'attachment'
			)
		}

		if ($hostname eq "www.tabroom.com"
			|| $hostname eq "profiler.tabroom.com"
		) {

			eval {
				$msg->send("sendmail", "/usr/sbin/sendmail -t -oi -odb");
			};

		} else {
			Tab::debuglog("Tab hostname is $hostname.  Not sending email from $from_string to $to_string");
		}
	}

	return;

</%init>
