<%args>
	$chapter
	$tourn => undef
</%args>
<%init>

	return unless $chapter->nsda;

	my $district = $chapter->district;

	return unless $district;

	my $limit;

	if ($tourn) {
		$limit = "and tourn.id = ".$tourn->id;
	} else {
		my $school_year = &Tab::school_year;
		$limit = "and tourn.start > '".DateTime::Format::MySQL->format_datetime($school_year)."'";
	}

    my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("

		select student.id, student.first, student.last,
			entry.id, entry.code,
			event.abbr, event.id, event.type,
			tourn.start, tourn.id,
			weekend.start, weekend.id

		from (tourn, tourn_setting,
				event, entry, school, entry_student, student,
				event_setting, weekend
			)

		where school.chapter = ?

			and school.tourn = tourn.id
			and tourn.id = tourn_setting.tourn
			and tourn_setting.tag = 'nsda_district'
			and tourn_setting.value = ?
			$limit

			and tourn.id = event.tourn
			and event.id = entry.event
			and entry.school = school.id
			and entry.id = entry_student.entry
			and entry.active = 1

			and entry_student.student = student.id

			and event_setting.event = event.id
			and event_setting.tag = 'weekend'
			and event_setting.value = weekend.id

		group by entry_student.id

	");

	$sth->execute(
		$chapter->id,
		$district->id,
	);

	my %district_entry = ();
	$district_entry{"house_total"}  = 0;
	$district_entry{"senate_total"} = 0;
	$district_entry{"entry_total"}  = 0;

	my %used;

	while (
		my (
			$student_id, $student_first, $student_last,
			$entry_id, $entry_code,
			$event_abbr, $event_id, $event_type,
			$tourn_start, $tourn_id,
			$weekend_start, $weekend_id
		) = $sth->fetchrow_array()
	) {

		if ($event_abbr eq "BQ") {
			$district_entry{"in_event"}{$student_id}{$event_id}++;
			$district_entry{"in_event"}{$student_id}{$event_abbr}++;
		} else {

			if ($event_abbr eq "HOU") {

				$district_entry{"house_count"}{$student_id}++;
				$district_entry{"house_total"}++ unless $used{$entry_id}++;

			} elsif ($event_abbr eq "SEN") {

				$district_entry{"senate_count"}{$student_id}++;
				$district_entry{"senate_total"}++ unless $used{$entry_id}++;

			} else {

				$district_entry{"event_count"}{$event_id}++;
				$district_entry{"student_count"}{$student_id}++;
				$district_entry{"entry_total"}++ unless $used{$entry_id}++;
			}

			push @{$district_entry{"entries"}{$student_id}}, $entry_id;
			push @{$district_entry{"entry_students"}{$entry_id}}, $student_id;

			$district_entry{"first_name"}{$student_id} = $student_first;
			$district_entry{"last_name"}{$student_id} = $student_last;

			$district_entry{"in_event"}{$student_id}{$event_id}++;
			$district_entry{"in_event"}{$student_id}{$event_abbr}++;

			$district_entry{"entry_event"}{$entry_id} = $event_id;
			$district_entry{"entry_event_type"}{$entry_id} = $event_type;
			$district_entry{"entry_event_abbr"}{$entry_id} = $event_abbr;

			$district_entry{"event_tourn"}{$event_id} = $tourn_id;
			$district_entry{"event_tourn"}{$event_abbr} = $tourn_id;

			$district_entry{"entry_tourn"}{$entry_id} = $tourn_id;
			$district_entry{"entry_weekend"}{$entry_id} = $weekend_id;
			$district_entry{"entry_event"}{$entry_id} = $event_id;

			$district_entry{"entry_time"}{$entry_id} = $weekend_start;
			$district_entry{"student_total"}{$student_id}++;
			$district_entry{"students"}{$student_id}++;

		}

	}

	my $bonus;
	my $event_count;

	foreach my $event (keys %{$district_entry{"event_count"}}) {
		$event_count++ if $district_entry{"event_count"}{$event} >= 2;
	}

	$district_entry{"entry_bonus"}++ if $event_count == 11;

	return %district_entry;

</%init>
