<%args>
	$tourn
	$tourn_settings
	$person
	$weekend_id          => undef
	$name                => undef
	$start               => undef
	$starttime           => undef
	$end                 => undef
	$endtime             => undef
	$reg_start           => undef
	$reg_starttime       => undef
	$reg_end             => undef
	$reg_endtime         => undef
	$drop_deadline       => undef
	$drop_deadlinetime   => undef
	$judge_deadline      => undef
	$judge_deadlinetime  => undef
	$freeze_deadline     => undef
	$freeze_deadlinetime => undef
	$fine_deadline       => undef
	$fine_deadlinetime   => undef
</%args>
<%init>

	my $weekend;

	my @messages;

	if ($weekend_id && $weekend_id ne "new_dates") {

		$weekend = Tab::Weekend->retrieve($weekend_id);

		if ($weekend) {

			$weekend->name($name);
			$weekend->city($ARGS{"city"});
			$weekend->state($ARGS{"state"});
			$weekend->update();

			# with Dennis Miller.

			push @messages, "Changed the location of ".$weekend->name." to ".$weekend->city." ".$weekend->state;

		}

	} else {

		$weekend = Tab::Weekend->create({
			tourn => $tourn->id,
			name  => $name,
			city  => $ARGS{"city"},
			state => $ARGS{"state"}
		});

		push @messages, "Created a new tournament weekend ".$weekend->name;

	}

	$m->abort() unless $weekend;

	my $tz = $tourn->tz;
	$tz = "UTC" unless $tz;

	my $startdt;
	my $enddt;

	my $reg_startdt;
	my $reg_enddt;

	eval {
		$startdt = Tab::dtme($start, $starttime, $tz);
		$enddt = Tab::dtme($end, $endtime, $tz);

		$reg_startdt = Tab::dtme($reg_start,$reg_starttime, $tz);
		$reg_enddt = Tab::dtme($reg_end,$reg_endtime, $tz);
	};

	unless ($startdt) {
		my $err = "Start date was not valid";
		$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
	}
	unless ($enddt) {
		my $err = "End date was not valid";
		$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
	}

	unless ($reg_startdt) {
		my $err = "Registration start date was not valid";
		$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
	}

	if ($tourn_settings->{"nsda_district"}) {

		my $seven_days_ago = $startdt->clone;
		$seven_days_ago->subtract(days => 7);

		if ($reg_enddt < $seven_days_ago
			&& !$person->site_admin
		) {

			my $err = "Registration must be due no less than 7 days before the start of your District tournament";
			$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
		}
	}

	unless ($reg_enddt) {
		my $err = "Registration deadline was not valid";
		$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
	}

	if ($startdt->year < 400 || $enddt->year < 400) {
		my $err = "Those tournament start or end dates are set to occur during the early Roman Empire.";
		$err .= "<br /><br />Please use full years (2014 instead of 14).";
		$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
	}

	if ($startdt->year < 2000 || $enddt->year < 2000) {
		my $err = "The year you chose for your start or end date ";
		$err .= "is several years in the past. <br /> <br />I doubt ";
		$err .= "that's what you want.  Try again?";
		$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
	}

	if ($startdt->year > 2100 || $enddt->year > 2100) {
		my $err = "The year you chose for your start or end date is ";
		$err .= "in the far distant future.<br />As much as I like to be an ";
		$err .= "optimist, I doubt Tabroom will be around that long.  Try again?";
		$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
	}

	if ($startdt->epoch > $enddt->epoch) {
		my $err = "Your tournament starts after it ends.  ";
		$err .= "The laws of time and space are not pleased at your arrogance.  ";
		$err .= "Please preserve the universe and try again.";
		$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&err=$err");
	}

	my $drop_deadlinedt;
	my $judge_deadlinedt;
	my $freeze_deadlinedt;
	my $fine_deadlinedt;

	eval {
		$drop_deadlinedt   = Tab::dtme($drop_deadline,$drop_deadlinetime, $tz);
		$judge_deadlinedt  = Tab::dtme($judge_deadline,$judge_deadlinetime, $tz);
		$freeze_deadlinedt = Tab::dtme($freeze_deadline,$freeze_deadlinetime, $tz);
		$fine_deadlinedt   = Tab::dtme($fine_deadline,$fine_deadlinetime, $tz);
	};

	$drop_deadlinedt = $reg_enddt->clone unless $drop_deadlinedt;
	$judge_deadlinedt = $reg_enddt->clone unless $judge_deadlinedt;
	$freeze_deadlinedt = $reg_enddt->clone unless $freeze_deadlinedt;
	$fine_deadlinedt = $reg_enddt->clone unless $fine_deadlinedt;

	if ($startdt != $weekend->start) {
		push @messages, "Changed the start date of ".$weekend->name." to ".Tab::shortdate($startdt);
	}

	if ($enddt != $weekend->end) {
		push @messages, "Changed the end date of ".$weekend->name." to ".Tab::shortdate($enddt);
	}

	$weekend->start($startdt);
	$weekend->end($enddt);
	$weekend->reg_start($reg_startdt);
	$weekend->reg_end($reg_enddt);

	$weekend->drop_deadline($drop_deadlinedt);
	$weekend->judge_deadline($judge_deadlinedt);
	$weekend->freeze_deadline($freeze_deadlinedt);
	$weekend->fine_deadline($fine_deadlinedt);

	my $site = Tab::Site->retrieve($ARGS{"site_id"}) if $ARGS{"site_id"};
	$weekend->site($site->id) if $site;
	$weekend->site("") unless $site;

	if ($site) {
		foreach my $event ($tourn->events() ) {

			next unless $event->setting('weekend') == $weekend->id;

			foreach my $round ($event->rounds() ) {
				$round->site($site->id);
				$round->update();

			}
		}
	}

	$weekend->update();

	my $tourn_startdt;
	my $tourn_enddt;
	my $tourn_regstartdt;
	my $tourn_regenddt;

	my %tourn_sites;

	foreach my $weekend ($tourn->weekends) {

		$tourn_startdt    = $weekend->start 	unless $tourn_startdt;
		$tourn_enddt      = $weekend->end 		unless $tourn_enddt;
		$tourn_regstartdt = $weekend->reg_start unless $tourn_regstartdt;
		$tourn_regenddt   = $weekend->reg_end 	unless $tourn_regenddt;

		$tourn_startdt    = $weekend->start 	if $tourn_startdt->epoch 	> $weekend->start->epoch;
		$tourn_enddt      = $weekend->end 		if $tourn_enddt->epoch 		< $weekend->end->epoch;
		$tourn_regstartdt = $weekend->reg_start if $tourn_regstartdt->epoch > $weekend->reg_start->epoch;
		$tourn_regenddt   = $weekend->reg_end 	if $tourn_regenddt->epoch 	< $weekend->reg_end->epoch;

		$tourn_sites{$weekend->site->id}++ if $weekend->site;

	}

	$tourn->start($tourn_startdt);
	$tourn->end($tourn_enddt);
	$tourn->reg_start($tourn_regstartdt);
	$tourn->reg_end($tourn_regenddt);

	$tourn->update();

	my %alreadies = map {$_->id => $_} $tourn->sites();

	foreach my $site_id (keys %tourn_sites) {

		next unless $site_id > 0;
		next if $alreadies{$site_id};

		my $site = Tab::Site->retrieve($site_id);

		next unless $site;

		Tab::TournSite->create({
			tourn => $tourn->id,
			site  => $site_id
		});

		$alreadies{$site_id} = $site;

	}

	foreach my $already (keys %alreadies) {

		next if $tourn_sites{$already};
		next if $site && $already == $site->id;

		foreach my $site_mem (
			Tab::TournSite->search(
				tourn => $tourn->id,
				site => 0
			)
		) {
			$site_mem->delete();
		}

		if ($alreadies{$already} && $alreadies{$already}->id) {

			my @site_mems = Tab::TournSite->search(
				tourn => $tourn->id,
				site => $already
			);

			Tab::Round->set_sql(by_site_and_tourn => "
				select round.*
				from round, event
				where round.event = event.id
				and event.tourn = ?
				and round.site = ?
			");

			my @site_rounds = Tab::Round->search_by_site_and_tourn($tourn->id, $already);

			if ($site) {
				foreach my $site_round (@site_rounds) {
					$site_round->site($site->id);
					$site_round->update();
				}
			}

			foreach my $mem (@site_mems) {
				$mem->delete();
			}

		}

	}

	foreach my $message (@messages) {

		$m->comp("/funclib/log.mas",
			tourn       => $tourn->id,
			type        => "districts",
			person      => $person->id,
			description => $message
		);
	}

	my $msg = "Date changes have been saved for ".$weekend->name." @messages ";

	$weekend->update();

	if ($tourn->setting("nsda_tabbing_software") eq "jot") {
		$msg .= $m->comp("/user/admin/nsda/update_dt_dates.mas",
			tourn => $tourn
		);
	}

	$m->comp("/funclib/update_calendar.mas", tourn => $tourn);

	$m->redirect("district_dates.mhtml?weekend_id=".$weekend->id."&msg=$msg");

</%init>
