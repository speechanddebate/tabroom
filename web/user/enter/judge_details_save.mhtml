<%args>
	$judge_id
	$school_id
	$alt_id       => undef
	$ada          => undef
	$parli        => undef
	$diverse      => 0
	$fyo          => 0
	$neutral      => 0
	$phone        => 0
	$email        => 0
	$qual_history => 0
	$free_strike  => 0
	$notes        => undef
	$rounds       => undef
</%args>
<%init>

	my $judge = Tab::Judge->retrieve($judge_id);

	$m->abort unless $judge;

	my $category = $judge->category;
	my $tourn = $category->tourn;

	my $err;

	my $rounds_per = $category->setting("rounds_per") if $category;

	my $now = DateTime->now;

	my $school = Tab::School->retrieve($school_id);

	my $missing_rating;

	if ($category->setting("coach_ratings")) { 

		unless ($category->rating_subsets) { 

			my $rating_id = $ARGS{"rating_id"};
			$rating_id = 0 unless $rating_id;
			my $rating = $judge->ratings->first;

			$missing_rating .= "<br/> Missing ".$category->name." rating " unless $rating_id;

			if ($rating) { 

				$rating->rating_tier($rating_id);
				$rating->update;

			} else { 
		
				Tab::Rating->create({
					type        => "coach",
					entered     => $now,
					tourn       => $tourn->id,
					judge       => $judge->id,
					rating_tier => $rating_id,
				});
			}

		} else { 

			foreach my $subset ($category->rating_subsets) { 

				my $tier_id = $ARGS{$subset->id};
				$tier_id = 0 unless $tier_id;

				$missing_rating .= "<br/> Missing ".$subset->name." rating." unless $tier_id;

				my $rating = $judge->ratings(rating_subset => $subset)->first;

				if ($rating) {	 

					$rating->rating_tier($tier_id);
					$rating->update;

				} else { 

					Tab::Rating->create({
						type          => "coach",
						tourn         => $tourn->id,
						judge         => $judge->id,
						rating_tier   => $tier_id,
						rating_subset => $subset->id
					});

				}
			}
		}

		if ($missing_rating) { 

			my $err = "You must rate the judge in every category. ".$missing_rating;
			$m->redirect("judge_details.mhtml?school_id=$school_id&judge_id=$judge_id&err=$err");

		}

	}
	
	my $min_pool_counter;

	if ($category->setting("min_registrant_jpools")) { 

		my %jpool_judges = 
			map {$_->jpool->id => $_} 
			Tab::JPoolJudge->search(judge => $judge->id);

		foreach my $jpool ($category->jpools()) { 

			next unless $jpool->setting("registrant");

			if ($ARGS{$jpool->id}) { 

				$min_pool_counter++;

				unless ($jpool_judges{$jpool->id}) { 

					Tab::JPoolJudge->create({
						judge => $judge->id,
						jpool => $jpool->id
					});
				}

			} else { 

				$jpool_judges{$jpool->id}->delete() 
					if $jpool_judges{$jpool->id};

			}

		}

	}


	$judge->obligation($rounds) if $rounds_per;
	$judge->alt_category($alt_id) if $alt_id;
	$judge->ada($ada);

	$judge->setting("parli", $parli);
	$judge->setting("diverse", $diverse);

	my $cj = $judge->chapter_judge;
	
	if ($cj 
		&& $judge 
		&& $cj->notes eq $judge->setting('notes') 
		&& $notes ne $cj->notes
	) { 
		$cj->notes($notes);
		$cj->notes_timestamp($now);
		$cj->update;
	}

	$judge->setting('notes', "text", $notes);

	$phone =~ s/[\D_]//g;
	$judge->setting("phone", $phone);
	$judge->setting("email", $email);

	my $judge_person = Tab::Person->search(email => $email)->first;

	unless ($judge_person) { 
		my $login = Tab::Login->search(username => $email)->first;
		$judge_person = $login->person if $login;
	}

	if ($judge_person) { 

		my @others = $school->judges(person => $judge_person->id);

		my $count;

		foreach my $other (@others) {
			next if $other->id == $judge->id;
			$count++;
		}

		unless ($count > 0) { 
		
			$judge->person($judge_person->id);
			$judge->update();

			$judge->setting("phone", $judge_person->phone) unless $phone;

			$m->comp(
				"/funclib/person_conflict.mas", 
				tourn  => $tourn,
				person => $judge_person
			);

		} else { 
	
			$err = "A judge from your school already has that email address.  Please only put in the judge's OWN tabroom account, not yours or someone else's!  Otherwise things will go a little haywire.";

		}

	}


	$judge->setting("qual_history", $qual_history);
	$judge->setting("first_year", $fyo);
	$judge->setting("neutral", $neutral);

	if ($fyo > 0 && $category->setting("fyo_free_strikes")) { 
		$judge->setting("free_strike", 1);
	} else { 
		$judge->setting("free_strike", $free_strike);
	} 

	my %strike_by_event = ();
	foreach my $strike (
		Tab::Strike->search( 
			type       => "event",
			judge      => $judge->id,
			registrant => 1
		)
	) { 
		$strike_by_event{$strike->event->id} = $strike;
	}

	EVENT:
	foreach my $event (
		$m->comp("/funclib/event_selfstrike.mas", 
			category => $category
		)
	) { 

		if ($ARGS{$event->id}) { 

			next EVENT if $strike_by_event{$event->id};

			Tab::Strike->create({
				judge      => $judge->id,
				event      => $event->id,
				tourn      => $tourn->id,
				type       => "event",
				registrant => 1
			});

		} else {

			my $strike = $strike_by_event{$event->id};
			$strike->delete if $strike;
		}

	}

	$judge->update();


	if (
		$min_pool_counter < $category->setting("min_registrant_jpools")
	) { 
		my $err = "All judges must be in a minimum of ".$category->setting("min_registrant_jpools")." pools";
		$m->redirect("judge_details.mhtml?school_id=$school_id&judge_id=$judge_id&err=$err");
	}

	my $msg = "Judge ".$judge->first." ".$judge->last." entered";
	$msg .= " and Tabroom account linked!" if $judge_person;

	$m->redirect("judges.mhtml?school_id=$school_id&category_id=".$category->id."&msg=$msg&err=$err");

</%init>
