<%args>
	$region
	$tourn
	$event_id => undef
	$person
</%args>
<%init>

    my $tz = $person->tz;
	$tz = "UTC" unless $tz;
	my $now = DateTime->now->set_time_zone($tz);

	my $event = Tab::Event->retrieve($event_id);
	my $quota = $region->setting('quota');

	my $max;
	if ($event) {
		$max = $event->setting("max_entry") || 1;
	}

	my @entries = $m->comp("/funclib/region_entries.mas",
		region => $region,
		event  => $event
	);

	@entries = sort {$a->id <=> $b->id} @entries;

	ENTRY:
	foreach my $step (1 .. $quota) {

		my $entry = 0;
		$entry = shift @entries if @entries;

		if ($entry) {

			if ($ARGS{"school_".$step} != $entry->school->id) {
				$entry->school($ARGS{"school_".$step});
				$entry->update();
			}

		} elsif ($ARGS{"school_".$step}) {

			$entry = Tab::Entry->create({
				tourn         => $tourn->id,
				event         => $event->id,
				school        => $ARGS{"school_".$step},
				registered_by => $person->id,
				created_at    => $now
			});
		}

		next unless $entry;

		if ($ARGS{"school_".$step}) {

			my @students = $entry->students;
			my %students_by_id = map { $_->id => $_ } @students;

			foreach my $stu (1 .. $max) {

				my $student_id;

				if ($ARGS{$step."_".$stu} eq "add") {

					my $student = Tab::Student->create({
						first     => $ARGS{$step."_".$stu."_first"},
						last      => $ARGS{$step."_".$stu."_last"},
						chapter   => $entry->school->chapter->id,
						grad_year => $tourn->start->year
					});

					$student_id = $student->id;

				} else {
					$student_id = $ARGS{$step."_".$stu};
					$student_id =~ s/[\D_]//g;
				}

				unless ($students_by_id{$student_id}) {
					Tab::EntryStudent->create({
						entry   => $entry->id,
						student => $student_id
					});
				}
			}

			$entry->name($m->comp("/funclib/entry_name.mas", entry => $entry));
			$entry->update();
		}
	}

	my $msg = "Entries saved in ".$event->name;

	$m->redirect("tourn_entries.mhtml?event_id=$event_id&region_id=".$region->id."&tourn_id=".$tourn->id."&msg=$msg");

</%init>
