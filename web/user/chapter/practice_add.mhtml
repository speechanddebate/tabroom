<%args>
	$person
	$person_settings
	$session
	$chapter_id => undef
	$default    => "calendar"
</%args>
<%init>

	my $tz = $person->tz;
	$tz = "UTC" unless $tz;

	my $chapter = Tab::Chapter->retrieve($chapter_id);

	unless ($chapter) {
		$m->comp("/funclib/abort.mas", message => "No chapter exists with ID $chapter_id");
	}

	my $access = Tab::Permission->search(
		person  => $person,
		chapter => $chapter->id
	)->first;

	unless ($person->site_admin || $access || $chapter_id) {
		$m->comp("/funclib/abort.mas", message => "You do you have access school ID $chapter_id");
	}

	unless ($chapter && $chapter->level) {
		my $err = "Please fill in your institution's level before continuing";
		$m->redirect("/user/chapter/settings.mhtml?chapter_id=".$chapter->id."&err=$err");
	}

	my $this_year = Tab::school_year;
	$this_year->add(years => 1);

	my $max = $this_year->year."-07-01";
	my $dbh = Tab::DBI->db_Main();

	my $now = DateTime->now();
	my $until = $now->clone();
	$until->add(months => 1);

	my @tabs = ("calendar", "create");

	my $start_dt = $m->comp("/funclib/showdt.mas",
		date   => $ARGS{"start_date"},
		time   => $ARGS{'start_time'},
		tz     => $tz,
		object => 1
	);

	my $end_dt = $m->comp("/funclib/showdt.mas",
		date   => $ARGS{"start_date"},
		time   => $ARGS{'end_time'},
		tz     => $tz,
		object => 1
	);

	unless ($start_dt && $end_dt) {
		$m->comp("/funclib/abort.mas", message => "You did not send a valid start or end time. Start $start_dt end $end_dt");
	}

	unless ($person->tz) {
		$m->comp("/funclib/abort.mas", message => "You do not have a timezone set in your account, so I do not know when to schedule practices.  Click your profile above and select a timezone.");
	}

	$start_dt->set_time_zone($person->tz);
	$end_dt->set_time_zone($person->tz);

	unless ($start_dt && $end_dt) {
		my $err = "Improperly formatted dates and times sent.  Please try again";
		$m->redirect("practice.mhtml?default=create&err=$err");
	}

	my $initial = Tab::Practice->create({
		chapter    => $chapter->id,
		name       => $ARGS{"label"},
		tag        => $ARGS{"tag"},
		start      => $start_dt,
		end        => $end_dt,
		created_by => $person,
	});

	my $count = 1;

	if ($ARGS{"recurring"}) {

		my $recur_stop = eval {
			my $recurstring = $ARGS{'recurring_end_date'}." 00:00:00";
			return DateTime::Format::MySQL->parse_datetime($recurstring);
		};

		unless ($recur_stop) {
			$recur_stop = DateTime::Format::MySQL->parse_datetime($max." 00:00:00");
		}

		while ($start_dt < $recur_stop) {

			if ($ARGS{"recurring"} eq "daily") {
				$start_dt->add(days => 1);
				$end_dt->add(days => 1);
				if ($start_dt->day_abbr eq "Sat") {
					$start_dt->add(days => 2);
					$end_dt->add(days => 2);
				} elsif ($start_dt->day_abbr eq "Sun") {
					$start_dt->add(days => 1);
					$end_dt->add(days => 1);
				}
			} else {
				$start_dt->add(weeks => 1);
				$end_dt->add(weeks => 1);
			}

			my $initial = Tab::Practice->create({
				chapter    => $chapter->id,
				name       => $ARGS{"label"},
				start      => $start_dt,
				end        => $end_dt,
				created_by => $person,
			});

			$count++;
		}
	}

	my $msg = $count." Practices added";
	$m->redirect("practice.mhtml?chapter_id=".$chapter->id."&msg=$msg");

</%init>

