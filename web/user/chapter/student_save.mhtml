<%args>
	$person
	$person_settings
	$chapter_id => undef
	$from       => undef
</%args>
<%init>

	my $err;
	my $msg;
	my $student;
	my $now = DateTime->now;

	foreach my $tag ("first", "middle", "last", "phonetic") {
		$ARGS{$tag} =~ s/^\s+//;
	}

	$ARGS{"novice"} = 0 unless $ARGS{"novice"};

	foreach my $tag ("first", "middle", "last") {

		# ALL CAPS IS FOR BOOMERS and you are not as cool as bell hooks and you
		# ruin my report sorting. So, capitalize like normal people.

		if (
			($ARGS{$tag} eq uc($ARGS{$tag}))
			|| ($ARGS{$tag} eq lc($ARGS{$tag}))
		) {

			$ARGS{$tag} = ucfirst(lc($ARGS{$tag}));
		}
	}

	foreach my $tag ("grad_year", "nsda") {
		$ARGS{$tag} =~ s/[\D_]//g;
	}

	unless ($ARGS{"first"} && $ARGS{"last"}) {
		$m->comp("/funclib/abort.mas", message => "WARNING: You have not supplied a first or last name.  Both are required.");
	}

	if ($ARGS{"grad_year"}) {

		my $now_year = $now->year;
		$now_year++ if $now->month > 6;

		unless ($person->site_admin) {
			unless ($ARGS{'grad_year'} >= $now_year) {
				$err = "WARNING: You have entered a graduation year in the past!  Be sure to put the whole year in, not just the last two digits. ";
				$m->comp("/funclib/abort.mas", message => $err);
			}

			if ($ARGS{'grad_year'} > ($now_year + 10)) {
				$err = "WARNING:  You have entered a graduation year far in the future!  Please correct to something more ordinary.";
				$m->comp("/funclib/abort.mas", message => $err);
			}
		}

		if ($ARGS{'grad_year'} < 200) {

			$ARGS{"grad_year"} += 2000;

			unless ($ARGS{"grad_year"} > $now_year && $ARGS{'grad_year'} < ($now_year + 10)) {
				$err = "You have entered an invalid graduation year. Please use complete years, not abbreviations.  Please try again";
				$m->comp("/funclib/abort.mas", message => $err);
			}
		}

	} else {
		my $now_year = $now->year;
		$now_year++ if $now->month > 6;
		$ARGS{"grad_year"} = $now_year unless $ARGS{"grad_year"};
	}

	if ($ARGS{"student_id"}) {

		$student = Tab::Student->retrieve($ARGS{"student_id"});

		unless ($student > 0) {
			$m->comp("/funclib/abort.mas", message => "No student found with ID ".$ARGS{"student_id"});
		}

		my $chapter = $student->chapter;

		my @acc = Tab::Permission->search(
			tag     => "chapter",
			person  => $person->id,
			chapter => $chapter->id
		);

		push (@acc, 1) if $person->site_admin;

		if (@acc) {

			$student->first($ARGS{"first"});
			$student->middle($ARGS{"middle"});
			$student->last($ARGS{"last"});
			$student->phonetic($ARGS{"phonetic"});
			$student->grad_year($ARGS{"grad_year"});
			$student->novice($ARGS{"novice"});
			$student->gender($ARGS{"gender"});


			$student->update();

			if ($person->site_admin || $person_settings->{"nsda_admin"}) {
				if ($student->nsda != $ARGS{"nsda"}) {
					$student = $m->comp("/funclib/nsda/student_link.mas", person => $person, student => $student, nsda => $ARGS{"nsda"});
				}
			}

			if ($student > 0) {
				$msg .= " Changes have been saved to ". $student->first." ".$student->last;
			}

		} else {

			$m->comp("/funclib/abort.mas",
				message => "You are not authorized to make that change.  No changes have been saved to student"
			);
		}

		$from = "edit";

	} else {

		$student = Tab::Student->create({
			first     => $ARGS{"first"},
			middle    => $ARGS{"middle"},
			last      => $ARGS{"last"},
			phonetic  => $ARGS{"phonetic"},
			grad_year => $ARGS{"grad_year"},
			gender    => $ARGS{"gender"},
			novice    => $ARGS{"novice"},
			nsda      => 0,
			chapter   => $chapter_id
		});

		if ($ARGS{"nsda"} && ($person->site_admin || $person_settings->{"nsda_admin"})) {
			$student = $m->comp("/funclib/nsda/student_link.mas", student => $student, nsda => $ARGS{"nsda"});
		}

		$msg = $student->first." ".$student->last." has been added to your school.";
	}

	if ($ARGS{"student_email"}) {
		$student->setting("student_email", lc($ARGS{"student_email"}));
	} else {
		$student->setting("student_email", 0);
	}

	if ($student->nsda > 0) {
		$m->comp("/funclib/nsda/membership.mas", student => $student);
	}

	if ($err || $from eq "edit") {
		$m->redirect("/user/chapter/student_edit.mhtml?student_id=".$student->id."&err=$err&msg=$msg");
	}

	$m->redirect("/user/chapter/student_edit.mhtml?chapter_id=".$chapter_id."&msg=$msg");

</%init>
