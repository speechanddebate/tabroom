<%args>
	$person
	$person_settings
	$perms
	$student_id     => undef
	$practice_id    => undef
	$property_value => undef
</%args>
<%init>

	$m->clear_buffer();
	$r->content_type('application/json');

	unless ($student_id) {
		$m->print('{ "error": true, "message": "No student ID sent"}');
		$m->abort();
	}

	my $student = Tab::Student->retrieve($student_id);

	unless ($student) {
		$m->print('{ "error": true, "message": "No student found for ID '.$student_id.'"}');
		$m->abort();
	}

	unless ($practice_id) {
		$m->print('{ "error": true, "message": "No practice ID sent"}');
		$m->abort();
	}

	my $practice = Tab::Practice->retrieve($practice_id);

	unless ($practice) {
		$m->print('{ "error": true, "message": "No practice found for ID '.$practice_id.'"}');
		$m->abort();
	}

	unless ($practice->chapter == $student->chapter) {
		$m->print('{ "error": true, "message": "That student does not belong to your school"}');
		$m->abort();
	}

	unless ($person->site_admin || $person_settings->{"nsda_admin"} ) {
		unless (
			$perms->{"chapter"}{$practice->chapter->id}
			&& $perms->{"chapter"}{$practice->chapter->id} eq "chapter"
		) {
			$m->print('{ "error": true, "message": "You do not have permission to modify that student"}');
			$m->abort();
		}
	}

	my %response;

	if ($property_value) {

		unless (Tab::PracticeStudent->search(practice => $practice->id, student => $student->id)->first) {

			Tab::PracticeStudent->create({
				practice   => $practice->id,
				student    => $student->id,
				created_by => $person->id
			});

			$response{"error"} = "false";
			$response{'message'} = $student->last." added to this practice";

		} else {

			$response{'message'} = $student->last." was already in this practice";
			$response{'refresh'} = 1;
			$response{"error"} = "true";
		}

	} else {

		my $already = Tab::PracticeStudent->search(practice => $practice->id, student => $student->id)->first;

		if ($already) {
			$already->delete();
			$response{'message'} = $student->last." removed from this practice";
			$response{"error"} = "false";
		} else {
			$response{'message'} = $student->last." was not in this practice";
			$response{'refresh'} = 1;
			$response{"error"} = "true";
		}
	}

	$m->print(JSON::encode_json(\%response));
	$m->abort();

</%init>

