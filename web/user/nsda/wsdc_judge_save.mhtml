<%args>
	$person
	$district_id        => undef
	$category_id        => undef
	$judge_id           => undef
	$email              => undef
	$school_id          => undef
	$original_school_id => undef
	$chapter_judge_id   => undef
</%args>
<%init>

	my $judge = Tab::Judge->retrieve($judge_id) if $judge_id;

	my $category = Tab::Category->retrieve($category_id);

	my $tourn = $category->tourn;
	my $chapter_judge;
	my $jperson;
	my $original_school;

	if ($chapter_judge_id) {
		$chapter_judge = Tab::ChapterJudge->retrieve($chapter_judge_id);
		$jperson = $chapter_judge->person;
	} elsif ($email) {
		$jperson = Tab::Person->search(email => $email)->first;
	}

	if (not defined $jperson) {
		my $err;
		if ($email) {
			$err = "No judge found with email $email";
		} elsif ($chapter_judge) {
			$err = "Judge does not have a linked Tabroom account";
		}
		$m->redirect("wsdc_judge_edit.mhtml?judge_id=$judge_id&school_id=$school_id&category_id=$category_id&email=$email&err=$err");
	}

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare('
		select
			judge.id, judge.first, judge.last, school.name school_name, category.abbr
		from (judge, category)
			left join school on school.id = judge.school
		where judge.person = ?
			and judge.category = category.id
			and category.tourn = ?
	');

	$sth->execute($jperson->id, $category->tourn->id);
	my $already = $sth->fetchall_hash();

	if (scalar @{$already} > 0) {
		my $err;
		foreach my $judge (@{$already}) {
			$err .= $judge->{first}." ".$judge->{last}." ".$judge->{email}." is already judging in ".$judge->{abbr};
			if ($judge->{school_name}) {
				$err .= " for ".$judge->{school_name};
			}

			$err .= ". Judges may not judge in Worlds and any other event at Nationals.";
		}
		$m->redirect("wsdc_judge_edit.mhtml?judge_id=$judge_id&school_id=$school_id&category_id=$category_id&email=$email&err=$err");
	}

	if ($jperson && (not defined $chapter_judge)) {
		$original_school = Tab::School->retrieve($original_school_id);

		unless ($original_school) {
			my $err = "Every Worlds judge must have an affiliated school, so we know where to include their registration details";
			$m->redirect("wsdc_judge_edit.mhtml?judge_id=$judge_id&school_id=$school_id&category_id=$category_id&email=$email&err=$err");
		}

		$chapter_judge = Tab::ChapterJudge->search(
			chapter => $original_school->chapter->id,
			person  => $jperson->id
		)->first;

		unless ($chapter_judge) {
			$chapter_judge = Tab::ChapterJudge->create({
				first   => $jperson->first,
				middle  => $jperson->middle,
				last    => $jperson->last,
				chapter => $original_school->chapter,
				retired => 0,
				person  => $jperson->id
			});
		}
	}

	unless ($chapter_judge) {
		my $err = "No chapter judge record found with that ID";
		$m->redirect("wsdc_judge_edit.mhtml?judge_id=$judge_id&category_id=$category_id&school_id=$school_id&email=$email&err=$err");
	}

	unless ($jperson) {
		my $err = "No tabroom account found for that email or judge.  All judges must be linked to a tabroom account";
		$m->redirect("wsdc_judge_edit.mhtml?judge_id=$judge_id&category_id=$category_id&school_id=$school_id&email=$email&err=$err");
	}

	Tab::Judge->set_sql( dupes => "
		select judge.*
			from judge, category
		where judge.category = category.id
			and category.tourn = ?
			and judge.person = ?
			and judge.id != ?
	");

	my @dupes;

	if ($tourn > 0 && $jperson > 0 && $judge > 0) {
		@dupes = Tab::Judge->search_dupes($tourn->id, $jperson->id, $judge->id);
	}

	if (@dupes) {
		my $err = "Tabroom account ".$jperson->email." is already registered to judge this tournament.";
		$m->redirect("wsdc_judge_edit.mhtml?judge_id=$judge_id&category_id=$category_id&school_id=$school_id&cj_id=$chapter_judge_id&email=$email&err=$err");
	}

	if ($judge) {

		$judge->chapter_judge($chapter_judge->id);
		$judge->first($chapter_judge->first);
		$judge->middle($chapter_judge->middle);
		$judge->last($chapter_judge->last);
		$judge->person($chapter_judge->person);
		$judge->category($category_id);
		$judge->update();

	} else {

		$judge = Tab::Judge->create({
			first         => $chapter_judge->first,
			middle        => $chapter_judge->middle,
			last          => $chapter_judge->last,
			person        => $chapter_judge->person,
			school        => $school_id,
			active        => 1,
			category      => $category_id,
			chapter_judge => $chapter_judge->id
		});
	}

	unless ($original_school) {
		$original_school = Tab::School->search(
			tourn   => $tourn,
			chapter => $chapter_judge->chapter
		)->first;
	}

	unless ($original_school) {
		my $err = "Every Worlds judge must have an affiliated school";
		$m->redirect("wsdc_judge_edit.mhtml?judge_id=$judge_id&category_id=$category_id&school_id=$school_id&email=$email&err=$err");
	}

	$judge->setting('original_school', $original_school->id);

	my $text = $category->setting("registration_email");

	if ($text) {

		my $district = Tab::District->retrieve($district_id);
		my $district_name = $district->name;
		$district_name =~ s/[\W_]//g;
		$district_name = lc($district_name);
		my $from_email = $district->name." <".$district_name.'@www.tabroom.com>';

		$m->comp("/funclib/judge_reg_email.mas",
			judge      => $judge,
			person     => $person,
			jperson    => $jperson,
			tourn      => $tourn,
			category   => $category,
			text       => $text,
			from_email => $from_email
		);
	}

	$m->comp("/funclib/nsda/membership.mas", person => $jperson);

	my $msg = "Judge details saved";

	$m->redirect("wsdc_judge_edit.mhtml?judge_id=".$judge->id."&category_id=$category_id&school_id=".$school_id."&msg=$msg");

</%init>
