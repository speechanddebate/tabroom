<%args>
	$person
	$session
	$defaults
	$first      => undef
	$middle     => undef
	$last       => undef
	$email      => undef
	$phone      => undef
	$provider   => undef
	$street     => undef
	$city       => undef
	$state      => undef
	$country    => undef
	$no_email   => 0
	$zip        => undef
	$postal     => undef
	$pronoun    => undef
	$timezone   => undef
	$please_stop_screaming => 0
</%args>
<%init>

	$m->abort unless $person;
	$m->abort unless $first;
	$m->abort unless $last;
	$m->abort unless $email;

	$email =~ s/\s+//g;
	$email = lc($email);

	my $token = $defaults->{"token"};

	unless ($token) {

		my $err = "No authentication token sent; please use only the profile form on Tabroom.";
		$m->redirect("/user/login/profile.mhtml?err=$err");

	} elsif ($token ne $ARGS{"token"}) {

		my $err = "Authentication token mismatch; please use only the profile form on Tabroom.";
		$m->redirect("/user/login/profile.mhtml?err=$err");
	}

	# Make sure the email is actually an email
	unless (Email::Valid->address( -address => $email, -mxcheck => 0 )) {
		my $err = "$email is not a validly formatted email";
		$m->redirect("/user/login/profile.mhtml?err=$err");
	}

	my $diff;

	if ($email ne lc($person->email)) {
		my $existing = Tab::Person->search(
			email => $email
		)->first;

		if ($existing) {
			my $err = "Email address $email already has an account belonging to: ";
			$err   .= $existing->first." ".$existing->last;
			$m->redirect("/user/login/profile.mhtml?err=$err");
		}

		Tab::log("EMAIL CHANGE: Account ID ".$person->id." changing to ($email)");
		$diff++;
	}

	if ($diff) {
		# Notify the old account so they can raise hell if this is wrong.

		my $subject = "Your Tabroom.com email address has been changed";

		my $msg = "\n\tHello! \n";

		$msg .= " Your Tabroom.com email address has been changed from ".$person->email." to ".$email." ";
		$msg .= "on your Tabroom profile.  If you did not attempt to make this change, presumably someone ";
		$msg .= "else has done so while logged into your Tabroom account.  Please notify us at ";
		$msg .= 'help@tabroom.com immediately if you did not authorize or want this change. ';

		my @raw;

		if ($person->phone && $person->provider) {

			push @raw, $person->phone.'@'.$person->provider;

			$m->comp("/funclib/send_notify.mas",
				array     => \@raw,
				subject => $subject,
				body    => $msg
			);
		}

		$msg .= "\n\n";
		$msg .= "Otherwise, all Tabroom notifications will now go to $email, and you must log into the site ";
		$msg .= "using that address as your login.\n";

		$msg .= "Thank you!\n";
		$msg .= "Tabroom.com, a project of the National Speech & Debate Assocation\n";
		$msg .= "https://www.tabroom.com\n";
		$msg .= "https://www.speechanddebate.org\n";

		undef @raw;
		push @raw, $person->email;
		push @raw, $email;

		$m->comp("/funclib/send_email.mas",
			raw     => \@raw,
			subject => $subject,
			body    => $msg
		);
	}

	$phone =~ s/[\D_]//g;

	$person->first($first);
	$person->middle($middle);
	$person->last($last);
	$person->street($street);
	$person->city($city);
	$person->email($email);
	$person->state($state);
	$person->country($country);
	$person->no_email($no_email);
	$person->zip($zip);
	$person->postal($postal);
	$person->pronoun($pronoun);
	$person->phone($phone);
	$person->provider($provider);
	$person->tz($timezone);
	$person->update();

	my $ldap;

	# Refresh the LDAP server
	$ldap .= $m->comp("/funclib/ldap_person.mas", person => $person);

	#lulz
	$person->setting("please_stop_screaming", $please_stop_screaming);

	delete $defaults->{"token"};
	$session->default($defaults);

	Tab::log("PROFILE CHANGE: Account ID ".$person->id." ($email) was modified from IP address ".$ENV{REMOTE_ADDR});
	Tab::log("LDAP Update yielded $ldap");

	my $msg = "Changes saved";

	if ($diff) {
		$m->comp("/funclib/email_confirm.mas",
			person => $person,
			send   => 1
		);
	}

	$m->redirect("/user/login/profile.mhtml?msg=$msg");

</%init>
