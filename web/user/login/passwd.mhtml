<%args>
	$person
	$session    => undef
	$oldpass    => undef
	$newpass    => undef
	$repeatpass => undef
	$defaults
</%args>
<%init>

	unless ($newpass && $repeatpass) {
		my $err = "You must enter a password to continue";
		$m->redirect("profile.mhtml?err=$err");
	}

	$m->abort unless $person;

	my $token = $defaults->{"token"};

	unless ($token) {

		my $err = "No authentication token sent. Please use only the password change form on Tabroom.";
		$m->redirect("/user/login/profile.mhtml?err=$err");

	} elsif ($token ne $ARGS{"token"}) {

		my $err = "Authentication token mismatch. Please use only the password change form on Tabroom.";
		$m->redirect("/user/login/profile.mhtml?err=$err");
	}

	my $err;

	if ($oldpass) {

		my $new_salt = $m->comp("/funclib/generate_randomstring.mas");

		unless ($newpass eq $repeatpass) {
			$err = "The two passwords you listed did not match";
		}

		my $change_ok;

		# This gets the salt from the MD5 combined password hash
		my $db_sha2_hash = $person->password;
		my $verify_password = crypt($oldpass,$db_sha2_hash);

		if ($db_sha2_hash eq $verify_password) {
			$change_ok++;
		} else {
			$err = "Old password was not correct";
		}

		if ($oldpass eq $newpass) {
			$err = "Your new password was the same as the current one.";

			if ($person->setting("force_password_change")) {
				$err .= "You must select a new password for this change";
			}
		}

		unless ($err) {

			my $now = DateTime->now();
			my $new_sha2_hash = crypt($newpass, '$6$' . $new_salt);

			$person->password($new_sha2_hash);
			$person->pass_timestamp($now);
			$person->update();

			$person->setting("force_password_change", 0);
			$person->setting("pass_change_expires", 0);
			$person->setting("pass_changekey", 0);

			foreach my $sesh ($person->sessions) {
				next if $sesh->id == $session->id;
				$sesh->delete();
			}

			Tab::log(
				"PASSWORD CHANGE: Person ID ".$person->id." (".$person->email.") "
				.$person->first." ".$person->last.
				" password was changed from IP address ".$ENV{REMOTE_ADDR}
			);

			my $subject = "Your Tabroom.com password has been changed";

			my $msg = "\n\tHello! \n";

			$msg .= "The password for your Tabroom.com account has been changed, ";
			$msg .= "presumably by you. ";
			$msg .= "All login sessions besides the current one have now expired. ";
			$msg .= "If you did NOT make this change, please notify us at ";
			$msg .= 'help@tabroom.com immediately. ';

			$msg .= "\n\n";
			$msg .= "Thank you!\n";
			$msg .= "Tabroom.com, a project of the National Speech & Debate Assocation\n";
			$msg .= "https://www.tabroom.com\n";
			$msg .= "https://www.speechanddebate.org\n";

			my @raw = [$person->id];

			my $numberwang = substr($now->epoch, -6, 6);
			my $from_string = "Tabroom.com Account <accounts_".$numberwang.'@www.tabroom.com>';

			$m->comp("/funclib/send_notify.mas",
				from    => $from_string,
				ids     => \@raw,
				subject => $subject,
				body    => $msg
			);
		}
	}


	my $msg = "Your password has been changed.  You may now log in.";
	$m->redirect("/user/login/profile.mhtml?err=$err&msg=$msg");

</%init>


