<%init>

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("
		select
			ps.id id,
			student.id student, student.first, student.middle, student.last,
			chapter.id chapter, chapter.name, naudl_updated.value_date updated,
			practice.id practice, practice.start, practice.end,
			practice.tag

		from (student, chapter, practice, practice_student ps, chapter_setting cs)

			left join student_setting naudl_updated
				on naudl_updated.student = student.id
				and naudl_updated.tag = 'naudl_updated'

		where 1=1

			and practice.reported = 0
			and practice.chapter  = chapter.id
			and practice.id       = ps.practice
			and ps.student        = student.id
			and chapter.id        = student.chapter
			and chapter.id        = cs.chapter
			and cs.tag            = 'naudl'

		order by practice.start DESC limit 50
	");

	$sth->execute();

	my $practices = $sth->fetchall_hash();
	my @results;
	my %new_students;
	my %cache;

	foreach my $practice (@{$practices}) {

		unless ($cache{$practice->{"practice"}}) {
			my $start_dt = DateTime::Format::MySQL->parse_datetime($practice->{"start"});
			my $end_dt = DateTime::Format::MySQL->parse_datetime($practice->{"end"});

			my $duration = $end_dt - $start_dt;
			$cache{$practice->{"practice"}} = $duration->in_units('minutes');
		}

		my $result = ({
			"tabroomid"       => "TR".$practice->{"student"},
			"teamid"          => "TR".$practice->{"chapter"},
			"practiceid"      => "TRP".$practice->{"practice"},
			"practiceDate"    => substr($practice->{"start"}, 0, 10),
			"duration"        => $cache{$practice->{"practice"}},
			"Engagement_Type" => $practice->{tag} || "Practice",
		});

		push @results, $result;

		unless ($practice->{"updated"}) {
			push @{$new_students{"students_from_tabroom"}},
				$m->comp("salesforce_student.mas", student_id => $practice->{"student"});
		}
	}

	return \@results, \%new_students;

</%init>
