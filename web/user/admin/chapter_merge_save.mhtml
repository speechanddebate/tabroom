<%args>
	$person
	$name
	$target_id => undef
</%args>
<%init>

	Tab::Entry->set_sql(school_merge => " update entry set school = ? where school = ? ");
	Tab::Judge->set_sql(school_merge => " update judge set school = ? where school = ? ");
	Tab::Strike->set_sql(school_merge => " update strike set school = ? where school = ? ");
	Tab::Fine->set_sql(school_merge => " update fine set school = ? where school = ? ");

	Tab::ChapterJudge->set_sql(name_judge_merge => "
		update judge,chapter_judge,school
		set judge.chapter_judge = chapter_judge.id
		where judge.chapter_judge is null
		and judge.school = school.id
		and school.chapter = ?
		and school.chapter = chapter_judge.chapter
		and chapter_judge.first = judge.first
		and chapter_judge.last = judge.last
	");

	my @candidates = Tab::Chapter->search_where( name => { "like", "%".$name."%"} );
	push @candidates, Tab::Chapter->retrieve($target_id) if $target_id;

	my $prefer;
	my @merge_me;
	my %merge_ids;

	if (ref($ARGS{"merge"}) eq "ARRAY") {
		%merge_ids = map { $_ => 1 } @{$ARGS{"merge"}};
	} else {
		$merge_ids{$ARGS{"merge"}}++;
	}

	foreach my $candidate (@candidates) {
		$prefer = $candidate if $ARGS{"prefer"} == $candidate;
		push @merge_me, $candidate if $merge_ids{$candidate};
	}

	$prefer = shift @merge_me unless $prefer;

	unless ($prefer) {
		$m->print("<p>No target for merging specificed</p>");
		$m->abort();
	}

	my %seen = ();
	@merge_me = grep { ! $seen{$_->id}++ } @merge_me;
	@merge_me = grep { $_ ne '' } @merge_me;
	@merge_me = grep { $_->id ne $prefer->id } @merge_me;

	my %admins = map {$_->id => $_} $prefer->admins;
	my %circuits = map {$_->id => $_} $prefer->circuits;
	my %tourns = map {$_->tourn->id => $_} $prefer->schools;

	my %prefer_settings = $prefer->all_settings;

	if ($ARGS{"exclude_".$prefer->id}) {
		foreach my $ca ($prefer->permissions) {
			$ca->delete;
		}
	}

	my $merge_string;

	foreach my $merge_me (@merge_me) {

		my $exclude = $ARGS{"exclude_".$merge_me->id};

		unless ($exclude) {

			foreach my $ca ($merge_me->permissions) {
				if ($admins{$ca->person->id}) {
					$ca->delete;
				} else {
					$admins{$ca->person->id}++;
					$ca->chapter($prefer->id);
					$ca->update;
				}
			}
		} else {
			foreach my $ca ($merge_me->permissions) {
				$ca->delete;
			}
		}

		foreach my $cc ($merge_me->chapter_circuits) {

			if ($circuits{$cc->circuit->id}) {
				$cc->delete;
			} else {

				eval{
					$cc->chapter($prefer->id);
					$cc->update;
				};
			}
		}

		foreach my $setting ($merge_me->settings) {
			next if $prefer_settings{$setting->tag};
			$setting->chapter($prefer->id);
			$setting->update;
		}

		my @fields = ("city", "state", "country", "level", "nsda");

		foreach my $field (@fields) {
			unless ($prefer->$field) {
				if ($merge_me->$field) {
					$prefer->$field($merge_me->$field);
					$prefer->update;
				}
			}
		}

		my @others = $merge_me->schools;

		foreach my $school ($merge_me->schools) {

			if ($tourns{$school->tourn->id}) {

				my $school_dest = $tourns{$school->tourn->id};
				Tab::Entry->sql_school_merge($school_dest->id, $school->id);
				Tab::Judge->sql_school_merge($school_dest->id, $school->id);
				Tab::Strike->sql_school_merge($school_dest->id, $school->id);
				Tab::Fine->sql_school_merge($school_dest->id, $school->id);
				$school->delete;

			} else {
				$school->chapter($prefer->id);
				$school->update;
			}

		}

		foreach my $student ($merge_me->students) {
			$student->chapter($prefer->id);
			$student->update;
		}

		foreach my $chapter_judge ($merge_me->chapter_judges) {
			$chapter_judge->chapter($prefer->id);
			$chapter_judge->update;
		}

		$merge_string .= " ID ".$merge_me->id." ".$merge_me->name;

		Tab::ChapterJudge->sql_name_judge_merge->execute($prefer->id);
		$merge_me->delete;

	}

	#Deduplicate the student roster
	my %students_by_name;

	foreach my $student ($prefer->students) {
		push @{$students_by_name{$student->first."-".$student->last}}, $student;
	}

	foreach my $key (keys %students_by_name) {
		next if scalar @{$students_by_name{$key}} < 2;
		my $destination = shift @{$students_by_name{$key}};

		foreach my $mergee (@{$students_by_name{$key}}) {
			$m->comp("/funclib/student_merge.mas",
				person      => $person,
				mergee      => $mergee,
				destination => $destination,
				check_grad  => 1
			);
		}
	}

	#Deduplicate the judge roster
	my %judges_by_name;

	foreach my $judge ($prefer->chapter_judges) {
		push @{$judges_by_name{$judge->first."-".$judge->last}}, $judge;
	}

	foreach my $key (keys %judges_by_name) {

		next if scalar @{$judges_by_name{$key}} < 2;
		my $destination = shift @{$judges_by_name{$key}};

		foreach my $mergee (@{$judges_by_name{$key}}) {
			$m->comp("/funclib/chapter_judge_merge.mas",
				person      => $person,
				mergee      => $mergee,
				destination => $destination
			);
		}

	}

	my $msg .= "School ".$prefer->name." has merged $merge_string into $prefer ".$prefer->name;

	$m->redirect("chapter_merge.mhtml?target_id=$target_id&name=$name&msg=$msg");

</%init>
