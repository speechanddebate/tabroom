<%args>
	$tourn
</%args>
<%init>

	use Tab::NSDA::DistrictDate;

	my $year = $tourn->start->year;
	$year-- if $tourn->start->month < 7;

	my $district_id = $tourn->setting("nsda_district");

	my %event_weekend = ();

	foreach my $event ($tourn->events() ) { 

		my $weekend = $event->setting('weekend');

		my $code = lc($event->abbr);

		# translate bullshit inconsistency
		$code = "dx" if $code eq "usx";
		$code = "cd" if $code eq "sen";
		$code = "cd" if $code eq "hou";

		$event_weekend{$code} = $weekend;

	}

	my $counter;

	foreach my $weekend (
		sort {$a->start->epoch <=> $b->start->epoch} $tourn->weekends() 
	) {

		$counter++;

		my @alreadys = Tab::NSDA::DistrictDate->search(
			weekend_id => $weekend->id
		);

		my $already;
		
		$already = shift @alreadys if @alreadys;

		foreach my $dupe (@alreadys) { 
			$dupe->delete() if $dupe;
		}

		if ($tourn->setting("nsda_jot_tabbed")) { 

			my $location;
			$location = $weekend->site->name;
			$location .= " " if $location;
			$location .= $weekend->city." " if $weekend->city;
			$location .= $weekend->state;

			if ($already) { 

				$already->sdate($weekend->start);
				$already->edate($weekend->end);
				$already->weekend($counter);
				$already->year($year);
				$already->location($location);

				$already->start_time($weekend->start->hour.":".$weekend->start->minute.":00");
				$already->end_time($weekend->end->hour.":".$weekend->end->minute.":00");


			} else { 

				$already = Tab::NSDA::DistrictDate->create({
					dist_id    => $district_id,
					weekend_id => $weekend->id,
					weekend    => $counter,
					sdate      => $weekend->start,
					edate      => $weekend->end,
					start_time => $weekend->start->hour.":".$weekend->start->minute.":00",
					end_time   => $weekend->end->hour.":".$weekend->end->minute.":00",
					year       => $year,
					location   => $location
				});

			}

			foreach my $code (keys %event_weekend) { 

				if ($event_weekend{$code} == $weekend->id) { 
					$already->$code("Y");
				} else {
					$already->$code("");
				}

			}

			$already->update();

		} else { 

			$already->delete() if $already;

		}
	}

	return " and JOT Dates table updated ";

</%init>
