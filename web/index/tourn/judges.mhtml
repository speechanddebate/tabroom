<%args>
	$tourn_id
	$category_id => undef
	$person      => undef
</%args>
<%init>

	unless ($tourn_id == int($tourn_id)) {
		$m->comp("/funclib/abort.mas", message => "Invalid or multiple tournament IDs sent");
	}

	unless ($category_id == int($category_id)) {
		$m->comp("/funclib/abort.mas", message => "Invalid or multiple category IDs sent");
	}

	my $key = $tourn_id."-".$category_id;

	return if $m->cache_self(
		key        => $key,
		expires_in => '30m'
	);

	my $tourn =  Tab::Tourn->retrieve($tourn_id);
	unless ($tourn) {
		$m->comp("/funclib/abort.mas",
			message => "Tournament ID $tourn_id is not valid"
		);
	}

	my %judges;
	my $category;
	my %category;

	if ($category_id) {

		my $dbh = Tab::DBI->db_Main();
		$category = Tab::Category->retrieve($category_id);

		unless ($category) {
			$m->comp("/funclib/abort.mas",
				message => "Category ID $category_id is not valid"
			);
		}

		my $sth = $dbh->prepare("
			select
				judge.id, judge.first, judge.middle, judge.last,
				judge.hired, judge.obligation, judge.active,
				hire_offer.value, hire_approved.value, self_registered.value,
				school.name, school.state, chapter.state, chapter.country,
				person.id, paradigm.id, person.state, person.country,
				rounds_per.value,
				max_rounds.value,
				online_hybrid.value,
				event.id online,
				category.id, category.name, category.abbr, nats_category.value
			from (judge, category)

				left join judge_setting online_hybrid
					on judge.id = online_hybrid.judge
					and online_hybrid.tag='online_hybrid'

				left join judge_setting hire_offer
					on judge.id = hire_offer.judge
					and hire_offer.tag='hire_offer'

				left join judge_setting hire_approved
					on judge.id = hire_approved.judge
					and hire_approved.tag='hire_approved'

				left join judge_setting self_registered
					on judge.id = self_registered.judge
					and self_registered.tag='self_registered'

				left join category_setting rounds_per
					on category.id = rounds_per.category
					and rounds_per.tag='rounds_per'

				left join category_setting max_rounds
					on category.id = max_rounds.category
					and max_rounds.tag='max_rounds'

				left join category_setting nats_category
					on category.id = nats_category.category
					and nats_category.tag='nats_category'

				left join school on judge.school = school.id
				left join chapter on school.chapter = chapter.id
				left join person on judge.person = person.id
				left join person_setting paradigm on paradigm.person = person.id and paradigm.tag = 'paradigm'

				left join event on event.category = category.id
					and exists (
						select es.id
							from event_setting es
						where es.event = event.id
							and es.tag = 'online_hybrid'
					)

			where category.id = ?
				and category.id = judge.category

			group by judge.id
		");

		$sth->execute($category->id);

		while (
			my (
				$id, $first, $middle, $last,
				$hired, $obligation, $active,
				$hire_offer, $hire_approved, $self_registered,
				$school_name, $school_state, $chapter_state, $chapter_country,
				$person_id, $paradigm_id, $person_state, $person_country,
				$rounds_per,
				$max_rounds,
				$online_hybrid,
				$event_online,
				$category_id, $category_name, $category_abbr, $nats_category,
			) = $sth->fetchrow_array()
		) {

			if ($self_registered && $active != 1) {
				next;
			}

			if ($hire_offer
				&& (not defined $hire_approved)
				&& ($obligation < 1)
				&& ($hired < 1)
			) {
				next;
			}

			if ($event_online) {
				$judges{$id}{"online"} = $online_hybrid;
				$category{"online"} = 1;
			}

			$judges{$id}{"last"}   = $last;
			$judges{$id}{"first"}  = $first;
			$judges{$id}{"middle"} = $middle;
			if ($middle) {
				$category{"middle"} = 1;
			}

			if ($school_name) {
				$judges{$id}{"school"} = $school_name;

				if ($chapter_state) {
					$judges{$id}{"state"} = $chapter_state;
				} else {
					$judges{$id}{"country"} = $chapter_country;
				}
			} else {

				if ($person_state) {
					$judges{$id}{"state"} = $person_state;
				} else {
					$judges{$id}{"country"} = $person_country;
				}
			}

			$judges{$id}{"person"} = $person_id;

			if ($paradigm_id) {
				$judges{$id}{"paradigm"} = 1;
			}

			$category{"name"} = $category_name;
			$category{"abbr"} = $category_abbr;
			$category{"nats"} = $nats_category;

			if ($rounds_per) {
				$category{"rounds_per"} = 1;
				$judges{$id}{"rounds"}  = ($hired + $obligation);

				if ($max_rounds > 0) {
					$category{"max_rounds"} = $max_rounds;
					if ($category{"max_rounds"} < $judges{$id}{"rounds"}) {
						$judges{$id}{"rounds"} = $category{"max_rounds"};
					}
				}
			}
		}

		$sth->finish();
		;
	}

    my $webpage = Tab::Webpage->search(
		tourn => $tourn_id,
		special => "judges"
	)->first;

</%init>

	<div class="main">

		<& "title.mas", tourn => $tourn &>

        <& "tabbar.mas",
			tourn => $tourn,
			person => $person
		&>

%		if ($category{"name"}) {

			<span class="threequarters nospace">
				<h5 class="semibold"><% $category{"name"} %>: <% scalar (keys %judges) %> Judges</h5>
			</span>

			<span
				class="rightalign quarter nospace"
				id="judgelist_buttonarea"
			>
			</span>

			<& "/funclib/tablesorter.mas",
				table => "judgelist"
			&>

			<table id="judgelist">

				<thead>

				<tr class="yellowrow">

					<th title="Paradigm" class="smaller">
						Paradigm
					</th>

					<th>
						First
					</th>

%					if ($category{"middle"}) {
						<th>
							Middle
						</th>
%					}

					<th>
						Last
					</th>

					<th>
						Institution
					</th>

					<th>
						Location
					</th>

%					if ($category{"online"}) {
						<th>
							Mode
						</th>
%					}

%					if ($category{"rounds_per"}) {
						<th>
							Rounds
						</th>
%					}

				</tr>

				</thead>

				<tbody>

<%perl>

				foreach my $id (
					sort {
						$judges{$a}{"last"} cmp $judges{$b}{"last"}
						|| $judges{$a}{"first"} cmp $judges{$b}{"first"}
					} keys %judges
				) {

</%perl>
					<tr>

						<td class="nospace centeralign">
%							if ($judges{$id}{"paradigm"}) {
								<a
									href="/index/paradigm.mhtml?judge_person_id=<% $judges{$id}{"person"} %>"
									class="buttonwhite bluetext fa fa-sm fa-file-text-o"
									target="_blank"
								></a>
%							}
						</td>

						<td class="nospace">
%						if ($judges{$id}{"paradigm"}) {
							<a class="white full padvert"
								href="/index/paradigm.mhtml?judge_person_id=<% $judges{$id}{"person"} %>"
							>
%						}
							<% $judges{$id}{"first"} %>
							</a>
						</td>

%						if ($category{"middle"}) {
							<td class="nospace">
%								if ($judges{$id}{"paradigm"}) {
									<a class="white full padvert"
										href="/index/paradigm.mhtml?judge_person_id=<% $judges{$id}{"person"} %>"
									>
%								}
									<% $judges{$id}{"middle"} %>
								</a>
							</td>
%						}

						<td class="nospace">
%							if ($judges{$id}{"paradigm"}) {
								<a class="white full padvert"
								href="/index/paradigm.mhtml?judge_person_id=<% $judges{$id}{"person"} %>"
								>
%							}
								<% $judges{$id}{"last"} %>
							</a>
						</td>

						<td>
%							if ($judges{$id}{"school"}) {
								<% $judges{$id}{"school"} %>
%							} else {
								<span class="hidden">1111111</span>
								Hire
%							}
						</td>

						<td class='centeralign'>
							<% $judges{$id}{"state"} %>
						</td>

%						if ($category{"online"}) {
							<td class='centeralign'>
								<% $judges{$id}{"online"}
									? '<span class="hiddencsv">1</span>'
									: '<span class="hiddencsv">0</span>'
								%>
								<% $judges{$id}{"online"}
									? '<i class="fa fa-lg bluetext fa-laptop" />'
									: ''
								%>
							</td>
%						}

%						if ($category{"rounds_per"}) {
							<td class="centeralign">
								<% $judges{$id}{"rounds"} %>
							</td>
%						}

					</tr>
%				}
				</tbody>
			</table>

%		} elsif ($webpage) {

	        <p>
				<% $webpage->content %>
			</p>

%		} else {

			<p class="martopmuchmore centeralign bigger semibold bluetext">
				Choose a judge category at right to see the judge list.
			</p>

			<p class="martopmuchmore centeralign bigger semibold bluetext">
				Tabroom only reports category lists which the tournament
				officials have released, <br />so you may not be able to see
				all judge categories.
			</p>

%		}

	</div>

	<& "menu.mas",
		category => $category,
		tourn    => $tourn
	&>

