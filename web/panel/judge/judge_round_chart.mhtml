<%args>
	$tourn
	$event_id
	$session
</%args>
<%init>

	my $event = Tab::Event->retrieve($event_id);
	my @rounds = sort {$a->name <=> $b->name} $event->rounds;
	my %span_by_round = ();

	foreach my $round (@rounds) {
		my $span = DateTime::Span->from_datetimes(
			start => $round->timeslot->start,
			end   => $round->timeslot->end
		);
		$span_by_round{$round->id} = $span;
	}

	my %judge_precluded;
	my @judges = sort {$a->last cmp $b->last} $event->category->judges;

	foreach my $judge (@judges) {

		foreach my $strike ($judge->strikes) {

			if ($strike->type eq "event" && $strike->event->id == $event_id) {
				$judge_precluded{$judge->id}{"event"}++;
			}

			if ($strike->type eq "time")  {

				my $strike_span = DateTime::Span->from_datetimes(
					start => $strike->start,
					end   => $strike->end
				);

				foreach my $round (@rounds) {
					$judge_precluded{$judge->id}{$round->id}++
						if $strike_span->intersects( $span_by_round{$round->id} );
				}
			}
		}
	}

    my $name = $event->name;
    $name =~ s/[\W_]//g;
	my $filename = "JudgeAvailability-".$name."-".$session->id;
	my $filepath = $Tab::file_root."/tmp/".$filename;

	$m->comp("/funclib/printout.mas",
		tourn    => $tourn,
		filename => $filename,
		head     => 1
	);

	open (TEXOUT, ">>$filepath.tex");

	print TEXOUT "\\noindent\n";
	print TEXOUT "{\\bf \\Large ". &Tab::texify($event->abbr)." \\hfill  Judge Round Preclusions }\n";
	print TEXOUT "\\smallskip\n";
	print TEXOUT "\\newline\n";

	my $tabular = "\\begin{tabular}{|p{1in}|p{1in}";
	foreach my $round (@rounds) {
		$tabular .= "|p{.15in}";
	}
	$tabular .= "|}\n";

	print TEXOUT "\\footnotesize\n";
	print TEXOUT $tabular;
	print TEXOUT "\\hline";
	print TEXOUT "\\rowcolor[rgb]{1,.95,.74}[5.5pt][5.5pt]\n";
	print TEXOUT "\\bf Judge  & \\bf School ";

	foreach my $round (@rounds) {
		my $label = $round->realname;
		$label =~ s/Round//g;
		$label =~ s/^\s+//;
		print TEXOUT " & \\bf ".substr($label,0,1);
	}

	print TEXOUT "\\\\ \\hline \n";
	print TEXOUT "\\end{tabular}\n\n";
	print TEXOUT "\\vspace{-1pt}\n";

	my $switch = 1;

	print TEXOUT "\\scriptsize \n";

	foreach my $judge (@judges) {

		print TEXOUT "\\noindent \n";
		print TEXOUT $tabular;
		print TEXOUT "\\hline\n";
		print TEXOUT "\\rowcolor[rgb]{.90,.90,.90}[5.5pt][5.5pt]\n" if $switch++ % 2;

		print TEXOUT "\\truncate{1.in}{";
		print TEXOUT &Tab::texify($judge->first." ".$judge->last)."} & ";
		print TEXOUT "\\truncate{1.in}{";
		print TEXOUT &Tab::texify($judge->school->short_name) if $judge->school;
		print TEXOUT &Tab::texify("Hired") unless $judge->school;
		print TEXOUT "} ";

		if ($judge_precluded{$judge->id}{"event"}) {
			print TEXOUT " & \\multicolumn{".scalar @rounds."}{|c|}{\\parbox{3in}{ Struck against event }} \n";
			print TEXOUT "\\\\ \\hline\n";
		} else {

			foreach my $round (@rounds) {
				print TEXOUT " & ";
				print TEXOUT "\\bf X " if $judge_precluded{$judge->id}{$round->id};
			}
		}

		print TEXOUT "\\\\ \\hline\n";
		print TEXOUT "\\end{tabular}\n\n";
		print TEXOUT "\\vspace{-1pt}\n";
	}

	$m->comp("/funclib/printout.mas",
		tourn    => $tourn,
		filename => $filename,
		tail     => 1 );

</%init>



