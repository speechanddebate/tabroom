<%args>
	$person
	$tourn
	$tourn_settings
	$panel_id       => undef
	$judge_id       => undef
	$steal          => undef
	$chair          => undef
</%args>
<%init>

	my $panel = Tab::Panel->retrieve($panel_id);
	my $judge = Tab::Judge->retrieve($judge_id);

	$m->abort unless $panel;
	$m->abort unless $judge;

	my $round = $panel->round();
	my $event = $round->event();

	$m->abort unless $round;

	my $came_from;
	my $msg;

	if ($steal) {

		Tab::Ballot->columns(TEMP => "panelid");

		Tab::Ballot->set_sql( steal_judge => "
			select ballot.*, panel.id as panelid
			from panel, ballot, round

			where round.timeslot = ?

			and panel.round  = round.id
			and panel.flight = ?
			and panel.id     = ballot.panel
			and ballot.judge = ?
		");

		my @ballots = Tab::Ballot->search_steal_judge(
			$round->timeslot->id,
			$panel->flight,
			$judge->id
		);

		my %done;

		foreach my $ballot (@ballots) {

			next if $done{$ballot->panelid}++;

			my $remove = Tab::Panel->retrieve($ballot->panelid);

			if ($remove) {
				$came_from = $remove->id;

				$m->comp("/funclib/judge_rm.mas",
					panel => $remove,
					judge => $judge
				);

				$msg = "Judge ".$judge->first." ".$judge->last." was swapped out of this section.  Please replace.";
			}
		}
	}

	my $wudc;
	$wudc++ if $event->type eq "wudc";

	$m->comp(
		"/funclib/panel_judgeadd.mas",
		panel => $panel,
		judge => $judge,
		wudc  => $wudc,
		chair => $chair
	);

	if ($event->type eq "congress") {

		if ($event->setting("po_contest")) {

			Tab::Panel->set_sql( po_section => "
				select panel.*
					from panel, round
				where round.event = ?
					and round.name = ?
					and round.id = panel.round
					and panel.letter = ?
			");

			my $po_panel = Tab::Panel->search_po_section(
				$event->setting("po_contest"),
				$round->name,
				$panel->letter
			)->first;

			if ($po_panel) {
				$m->comp(
					"/funclib/panel_judgeadd.mas",
					panel => $po_panel,
					judge => $judge,
					wudc  => $wudc,
					chair => $chair
				);
			}
		}
	}

	my $regline = "Judge ".$judge->last." moved into section ".$panel->letter." round ".$round->realname." of ".$event->abbr;

	$m->comp("/funclib/log.mas",
		type        => "judge",
		judge       => $judge->id,
		tourn       => $tourn->id,
		new_panel   => $panel->id,
		description => $regline,
		event       => $event->id,
		person      => $person->id
	);

	$msg .= "Judge ".$judge->first." ".$judge->last." has been added. <br />";

	return if $ARGS{"return"};

	$m->redirect("/panel/schemat/panel_view.mhtml?panel_id=$panel_id&came_from=$came_from&taken=".$judge->id."&msg=$msg");

</%init>
