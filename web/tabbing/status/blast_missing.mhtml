<%args>
	$tourn
	$person
	$target_id      => undef
	$related_thing  => undef
	$another_thing  => undef
	$setting_name   => undef
	$property_name  => undef
</%args>
<%init>

	$m->clear_buffer();
	$r->content_type('application/json');

	unless ($target_id) {
		$m->print('{ "error": true, "message": "No Panel ID sent"}');
		$m->abort();
	}

	my $panel = Tab::Panel->retrieve($target_id);

	unless ($panel) {
		$m->print('{ "error": true, "message": "No Panel was found for ID '.$target_id.'"}');
		$m->abort();
	}

	my $dbh = Tab::DBI->db_Main();

	my $event_abbr;
	my $round_name;
	my $round_flighted;
	my $room_name;
	my $flight_name;

	my $entries_sth = $dbh->prepare("
		select
			person.last,
			person.id, person.email,
			round.name, round.label, round.flighted,
			event.abbr, panel.letter, room.name,
			dont_poke.value

		from (ballot, entry, entry_student es, student, person, round, event, panel)

			left join room on room.id = panel.room
			left join event_setting dont_poke
				on dont_poke.event = event.id
				and dont_poke.tag = 'dont_poke_entries'

		where ballot.panel = ?
			and ballot.entry = entry.id
			and entry.active = 1
			and entry.id = es.entry
			and es.student = student.id
			and student.person = person.id
			and person.no_email != 1
			and entry.event = event.id
			and panel.id = ballot.panel
			and round.id = panel.round
		group by person.id
	");

	my %ids;

	$entries_sth->execute($panel->id);

	while (
		my (
			$last,
			$person_id, $email,
			$round_num, $round_label, $flighted,
			$event,
			$letter, $room,
			$dont_poke
		) = $entries_sth->fetchrow_array()
	) {

		unless($event_abbr) {
			if ($event) {
				$event_abbr = $event;
			}

			if ($flighted > 1) {
				$round_flighted++;
			}

			if ($round_label) {
				$round_name = $round_label;
			} elsif ($round_num) {
				$round_name = "Round ".$round_num;
			}

			if ($room) {
				$room_name = $room;
			} elsif ($letter) {
				$room_name = "Section ".$letter;
			}
		}

		unless ($dont_poke) {
			$ids{$person_id}++;
		}
	}

	my $judges_sth = $dbh->prepare("
		select
			person.last,
			person.id, person.email

		from (ballot, judge, person)

		where ballot.panel = ?
			and ballot.judge = judge.id
			and judge.person = person.id
			and person.no_email != 1
		group by person.id
	");

	$judges_sth->execute($panel->id);

	while (
		my (
			$last,
			$id, $email
		) = $judges_sth->fetchrow_array()
	) {
		$ids{$id}++;
	}

	my $log_sth = $dbh->prepare("
		select
			cl.panel, cl.tag, cl.description, cl.person, cl.entry, cl.judge,
				CONVERT_TZ(cl.timestamp, '+00:00', tourn.tz),
			person.id, person.first, person.last

		from panel, campus_log cl, tourn, person, round

		where panel.id = ?

			and panel.round = round.id
			and panel.id = cl.panel
			and cl.tourn = tourn.id
			and cl.person = person.id

			and ( exists (
					select ballot.id
						from ballot, judge
					where judge.id = ballot.judge
						and judge.person = person.id
						and ballot.panel = panel.id
				) or exists (
					select ballot.id
						from ballot, entry_student es, student
					where ballot.panel = panel.id
						and ballot.entry = es.entry
						and es.student = student.id
						and student.person = person.id
				)
			)
		order by cl.timestamp
	");

	$log_sth->execute($panel->id);

	my %ok;

	while (
		my (
			$panel_id, $tag, $desc, $person, $entry, $judge, $timestamp,
			$id, $first, $last
		) = $log_sth->fetchrow_array()
	) {

		if ($tag eq "absent") {
			delete $ok{$person};
		} else {
			$ok{$person}++;
		}
	}

	foreach my $okey (keys %ok) {
		delete $ids{$okey};
	}

	my $counter;
	my @reclass;

	foreach my $id (keys %ids) {
		$counter++;

		my %limit = ();
		$limit{"id"} = "blast_".$target_id;
		$limit{"addClass"} = "hidden";
		push @reclass, \%limit;
	}

	my $message = "You are currently marked as not present in $round_name of $event_abbr in $room_name.";

	if ($round_flighted) {
		$message .= " (Flight ".$panel->flight.") ";
	}

	$message .= " Please go to $room_name now so the round may proceed.";

	my $now = DateTime->now();
	my $numberwang = substr($now->epoch, -6, 6);
	my $from_string = $tourn->name." <".$tourn->webname.'_'.$numberwang.'@www.tabroom.com>';

	my @ids = keys %ids;

	$m->comp("/funclib/send_notify.mas",
		from    => $from_string,
		tourn   => $tourn,
		ids     => \@ids,
		subject => "Missing from $room_name",
		body    => $message
	);

	$log_sth->finish;

	my $response = {
		error   => \0,
		message => $counter." missing people blasted in ".$room_name,
		reclass => \@reclass
	};

	$m->print(JSON::encode_json($response));
	$m->abort();

</%init>
