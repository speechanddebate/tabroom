<%args>
	$tourn
	$tourn_settings
	$person
	$person_settings
	$perms
	$sweep_set_id => undef
	$what         => undef
</%args>
<%init>

	if ($what eq "Print") {
		$m->redirect("sweep_schools_print.mhtml?sweep_set_id=$sweep_set_id");
	}

	my @sweep_sets;
	my $ncfl++ if $tourn_settings->{"ncfl"};
	my $regions++ if $tourn_settings->{"regions"};

	my $master_set = Tab::SweepSet->retrieve($sweep_set_id);

	$m->print("No sweepstakes set selected") unless $master_set;

	$m->abort() unless $master_set;

	my %schools = $m->comp(
		"/tabbing/results/sweep_schools.mas",
		sweep_set   => $master_set,
		all_entries => 1
	);

	my $cumulatives = $master_set->rules(tag => "cumulative")->first;

	my %nsda_points;
	my %cumulative_points;

	if ($cumulatives) {
		%nsda_points = $m->comp(
			"/funclib/nsda/sweeps.mas",
			award => $cumulatives->value,
			year  => ($tourn->start->year - 1)
		);
	}

	my $dbh = Tab::DBI->db_Main();

	my %last_round;
	my %area_entries;
	my %used_events;
	my $school_event_sth;

	if ($tourn_settings->{nsda_nats}) {

		my $round_sth = $dbh->prepare("
			select event.id, round.name, round.type, round.label
			from event, round
			where event.tourn = ?
				and event.id = round.event
		");

		$round_sth->execute($tourn->id);

		my %round_type;

		while (
			my (
				$event_id, $round_name, $round_type, $round_label
			) = $round_sth->fetchrow_array()
		) {
			$round_type{$event_id}{$round_name}{"type"} = $round_type;
			$round_type{$event_id}{$round_name}{"label"} = $round_label;
		}


		my $sth = $dbh->prepare("
			select entry.id, entry.event, max(round.name)
			from entry, ballot, panel, round, event
			where event.tourn = ?
				and event.id = round.event
				and round.id = panel.round
				and panel.id = ballot.panel
				and ballot.entry = entry.id
				and entry.active = 1
			group by entry.id
			order by round.name desc
		");

		$sth->execute($tourn->id);

		while (
			my ($entry_id, $event_id, $round_name) = $sth->fetchrow_array()
		) {
			$last_round{$entry_id}{'name'} = $round_name;
			$last_round{$entry_id}{'type'} = $round_type{$event_id}{$round_name}{"type"};
			$last_round{$entry_id}{'label'} = $round_type{$event_id}{$round_name}{"label"};
		}

		$sth->finish();

		my @events;

		my @sets;
		push @sets, $master_set;

		while (@sets) {
			my $set = shift @sets;
			push @sets, $set->children() if $set->children();
			push @events, $set->events();
		}

		%used_events = map{ $_->id => $_} @events;

		my $event_string = join(",", keys %used_events);

		$school_event_sth = $dbh->prepare("
			select school.id, count(entry.id), chapter.nsda
			from entry, school, chapter
			where school.tourn = ?
				and entry.school = school.id
				and entry.unconfirmed = 0
				and entry.event in ($event_string)
				and school.chapter = chapter.id
				and not exists (
					select dq.id
					from entry_setting dq
					where dq.entry = entry.id
					and dq.tag = 'dq'
				)
			group by school.id
		");

		$school_event_sth->execute($tourn->id);

		while (
			my ($school_id, $count, $nsda) = $school_event_sth->fetchrow_array()
		) {
			$area_entries{$school_id} = $count;

			if ($nsda_points{$nsda}) {
				$cumulative_points{$school_id} = $nsda_points{$nsda}{"points"};
				if ($nsda_points{$nsda}{"official"}) {
					$schools{$school_id}{"name"} = $nsda_points{$nsda}{"official"};
				}
			}
		}

		$school_event_sth->finish();
	}

	;

</%init>

	<& "menu.mas",
		tourn           => $tourn,
		tourn_settings  => $tourn_settings,
		person          => $person,
		person_settings => $person_settings,
		perms           => $perms,
		part            => "sweeps",
		sweep_set_id    => $sweep_set_id,
		whoami          => "sweep_schools"
	&>

	<div class="main">

		<div class="top">
			<span class="half nospace">
				<h5><% $master_set->name %> by Institution</h5>
			</span>

			<span class="quarter centeralign padtop martopless">
				<form
					action = "sweep_schools.mhtml"
					method = "post"
				>
					<input
						type  = "hidden"
						name  = "what"
						value = "<% $what %>"
					>

					<select
						name     = "sweep_set_id"
						class    = "fixedmost"
						onchange = 'this.form.submit();'
					>
%						foreach my $set (sort {$a->name cmp $b->name} $tourn->sweep_sets) {
							<option
								<% $set->id eq $sweep_set_id ? 'selected="selected"' : "" %>
								value="<% $set->id %>"
							> <% $set->name %> </option>
%						}
					</select>
				</form>
			</span>

			<span
				id    = "sweeps_table_buttonarea"
				class = "quarter nospace rightalign"
			>

				<script>
					function toggleAudit() {
						$(".audit").toggleClass('hidden');
						$("#auditButton").toggleClass('fa-eye-slash');
						$("#auditButton").toggleClass('invert');
						$("table").trigger("applyWidgets");
					}
				</script>

%				if ($what eq "Audit") {
					<a
						id      = "auditButton"
						class   = "buttonwhite orangetext fa fa-sm fa-eye"
						onClick = "toggleAudit();"
						title   = "Show/Hide Audit"
					></a>
%				}

				<a
					class="buttonwhite bluetext fa fa-sm fa-file-pdf-o"
					href="sweep_schools_print.mhtml?sweep_set_id=<% $master_set->id %>"
				></a>
			</span>

			<div class="full centeralign padless marno explain">
				Hover over a school to see breakdown
			</div>

%			if ($what eq "Audit" && $tourn_settings->{"nsda_nats"}) {
				<div class="full centeralign padless marno explain">
					Remember for Nationals the "National Places" report under
					Results &ndash;&gt; Web Publish must exist for this report
					to be correct.

					<br />  It does not need to be final; as long as it
					reflects the top 6 in IE/Debate.
				</div>
%			}
		</div>

		<& "/funclib/tablesorter.mas", table => "sweeps_table" &>

		<table id="sweeps_table">
			<thead>
				<tr class="yellowrow">

					<th class="smallish">
					</th>

					<th class="smallish">
						Tie
					</th>

					<th class="smallish">
						School
					</th>

%					if ($ncfl) {
						<th class="smallish">
							Diocese
						</th>
%					} elsif ($regions) {
						<th class="smallish">
							Region
						</th>
%					} else {
						<th class="smallish">
							State
						</th>
%					}

					<th class="smallish">
						Total Entries
					</th>

					<th class="smallish">
						Counted Entries*
					</th>

%					if ($tourn_settings->{nsda_nats}) {
						<th class="smallish">
							Entries in Area
						</th>
%					}

%				 	if ($cumulatives) {

						<th class="smallish">
							This Year
						</th>

						<th class="smallish">
							Cumulated
						</th>

						<th class="smallish">
							Total
						</th>

%					} else {

						<th class="smallish">
							Average
						</th>

						<th class="smallish">
							Total
						</th>
%					}
				</tr>

			</thead>
			<tbody>
<%perl>

				my $count = 1;
				my $place;
				my $last_points;
				my $add_to_place;

				my %tiers;

				foreach my $school_id (keys %schools) {
					next unless $schools{$school_id}{"points"};
					next unless $schools{$school_id}{"counted_entry_count"};

					$schools{$school_id}{"average"}
						= sprintf("%.2f", ($schools{$school_id}{"points"} / $schools{$school_id}{"counted_entry_count"}));

				 	if ($cumulatives) {
						$schools{$school_id}{"cumulative"}
							= sprintf("%.2f", ($schools{$school_id}{"points"} + $cumulative_points{$school_id}));
					}
				}

				foreach my $school_id ( sort {
					$schools{$b}{"cumulative"} <=> $schools{$a}{"cumulative"}
					|| $schools{$b}{"points"} <=> $schools{$a}{"points"}
					|| $schools{$b}{"average"} <=> $schools{$a}{"average"}
				 	} keys %schools
				) {

					next unless $schools{$school_id}{"points"};

					if ($last_points == $schools{$school_id}{"points"}) {
						$add_to_place++;
					} elsif ($add_to_place) {
						$place++;
						$place += $add_to_place;
						undef $add_to_place;
					}  else {
						$place++;
					}

					$last_points = $schools{$school_id}{"points"};
					push @{$tiers{$place}}, $school_id;
				}

				my $was_tie;

				foreach my $place (sort {$a <=> $b} keys %tiers) {

					my $tie;

					$tie++ if scalar @{$tiers{$place}} > 1;

					my $border;

					if ($tie > 0 && $was_tie > 0) {
						$border++;
					} elsif ($tie > 0) {
						$was_tie++;
					} else {
						undef $was_tie;
					}

					my $notfirst;

					foreach my $school_id (@{$tiers{$place}}) {
</%perl>

						<tr
							class = "hover <% $border ? "ltbordertop" : "" %>"
							title = "<% $schools{$school_id}{"count_string"} %>"
						>

%							undef $border;

							<td class="smallish centeralign">
								<% $place %>
							</td>

							<td class="smallish centeralign">
								<% $tie ? "T" : "" %>
							</td>

							<td class="smallish">
								<a
									href   = "/register/school/entries.mhtml?school_id=<% $school_id %>"
									class  = "plain full"
									target = "_blank"
								><% $schools{$school_id}{"name"} %></a>
							</td>

							<td class="smallish" title="<% $schools{$school_id}{"regionname"} %>">
								<% ($regions || $ncfl)
									? $schools{$school_id}{"regioncode"}
									: $schools{$school_id}{"state"}
								%>
							</td>

							<td class="smallish rightalign">
								<% $schools{$school_id}{"all_entry_count"} %>
							</td>

							<td class="smallish rightalign">
								<% $schools{$school_id}{"counted_entry_count"} + $schools{$school_id}{"wildcard_count"} %>
							</td>

%							if ($tourn_settings->{nsda_nats}) {
								<td class="smallish rightalign">
									<% $area_entries{$school_id} %>
								</td>
%							}

%					 		unless ($cumulatives) {
								<td class="smallish rightalign">
									<% $schools{$school_id}{"average"} %>
								</td>
%							}

							<td class="rightalign semibold bluetext">
								<% $schools{$school_id}{"points"} %>
								<span class="halfspacer"></span>
							</td>

%					 		if ($cumulatives) {

								<td class="smallish rightalign">
									<% $cumulative_points{$school_id} %>
								</td>

								<td class="rightalign semibold bluetext">
									<% ($schools{$school_id}{"points"} + $cumulative_points{$school_id}) %>
									<span class="halfspacer"></span>
								</td>
%							}

						</tr>
<%perl>

						if ($what eq "Audit") {

							foreach my $entry_id (
								sort {
									$schools{$school_id}{"entries"}{$b}{"points"}
									<=> $schools{$school_id}{"entries"}{$a}{"points"}
								} keys %{$schools{$school_id}{"entries"}}
							) {
</%perl>
								<tr class="audit">
									<td>
										<span class="hidden"><% $place %></span>
									</td>

									<td class="smallish">
										<span class="hidden"><% $schools{$school_id}{"name"} %></span>
									</td>

									<td class="smallish">
									</td>

									<td class="smallish">
										<a
											href   = "/register/entry/edit.mhtml?entry_id=<% $entry_id %>"
											class  = "plain full"
											target = "_blank"
										><% $schools{$school_id}{"entries"}{$entry_id}{"code"} %></a>
									</td>

									<td class="smallish">
										<% $schools{$school_id}{"entries"}{$entry_id}{"event"} %>
									</td>

									<td class="smallish">
										Rd <% $last_round{$entry_id}{"name"} %>
									</td>

									<td class="smallish">
										<%
											$last_round{$entry_id}{"label"}
											? ucfirst($last_round{$entry_id}{"label"})
											: ucfirst($last_round{$entry_id}{"type"})
										%>
									</td>


									<td class="smallish rightalign
										<%
											$schools{$school_id}{"entries"}{$entry_id}{"points"}  > 0
											? "semibold bluetext"
											: "graytext"
										%>
									">
										<% $schools{$school_id}{"entries"}{$entry_id}{"points"} %>
										<span class="halfspacer"></span>
									</td>

%									if ($cumulatives) {

										<td class="smallish rightalign">
											<span class="hidden">
												<% $cumulative_points{$school_id} %>
											</span>
										</td>

										<td class="rightalign semibold bluetext">
											<span class="hidden">
											<% ($schools{$school_id}{"points"} + $cumulative_points{$school_id}) %>
											</span>
										</td>
%									}

								</tr>
%							}

							<tr class="audit">
								<td colspan="10">
									<span class="hiddencsv">-</span>
								</td>
							</tr>

%						}
%					}
%					$was_tie++ if $tie;
%				}
			</tbody>
		</table>
	</div>

