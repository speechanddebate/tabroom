<%args>
	$tourn
	$person
	$judge_id
	$panel_id
	$all => undef
	$display => undef
</%args>
<%init>

	my $msg;
	my $err;
	my $ballot_err;
	my $rank_string;

	my $judge = Tab::Judge->retrieve($judge_id);
	my $panel = Tab::Panel->retrieve($panel_id);

	my @ballots = Tab::Ballot->search( judge => $judge_id, panel => $panel_id);
	my @scores = $m->comp('/funclib/panel_scores.mas', panel => $panel, judge => $judge);

	my %values_by_ballot = ();
	foreach my $value (@scores) {
		push @{$values_by_ballot{$value->ballot->id}}, $value;
	}

	my $point_increments = $panel->round->event->setting("point_increments") if $panel;
	my $min = $panel->round->event->setting('min_points') if $panel;  
	my $max = $panel->round->event->setting('max_points') if $panel;

	my %team_rank;
	my %team_points;
	my %speaker_points;
	my %already;

	if ($panel->round->type eq "elim") {

		my @entries = $m->comp("/funclib/panel_entries.mas", panel => $panel);

		my %used;
		my @winners;
		my @winner_codes;
		my %positions;

		foreach my $entry (@entries) {
		
			my $position = $ARGS{"position_".$entry->id};

			if ($position < 1 || $position > 4) { 

				$err = "You did not specify a position for ".$entry->code;

			} elsif ($used{$position}) {

				$err = "You assigned two entries to ";
				$err .= "1st Gov" if $position == 1;
				$err .= "1st Opp" if $position == 2;
				$err .= "2nd Gov" if $position == 3;
				$err .= "2nd Opp" if $position == 4;

			} else { 

				$positions{$entry->id} = $position;
				$used{$position}++;

				if ($ARGS{"advances_".$entry->id}) { 
					push @winners, $entry->id;
					push @winner_codes, $entry->code;
				}

			}
		}

		$err = "You have advanced ".scalar @winners." and not 2.  Try again." if scalar @winners != 2;

		unless ($err) { 
			
			my %entry_winner = map {$_ => 1} @winners;

			foreach my $ballot (@ballots) {
		
				foreach my $value (@{$values_by_ballot{$ballot->id}}) { 
					$value->delete;
				}

				my $entry = $ballot->entry->id;

				$ballot->speakerorder($positions{$entry});
				$ballot->audit("1");
				$ballot->update;

				if ($entry_winner{$entry}) { 

					Tab::Score->create({
						ballot => $ballot->id,
						tag    => "ballot",
						value  => 1
					});

				} else { 

					Tab::Score->create({
						ballot => $ballot->id,
						tag    => "ballot",
						value  => 0
					});

				}
			}

			$msg = "Entry finished.<br />";

			my $notfirst;
			
			foreach my $code (@winner_codes) { 
				$msg .= " and " if $notfirst++;
				$msg .= $code;
			}
			
			$msg .= " advanced.";

		}
	
		$m->redirect("../index.mhtml?all=$all&timeslot_id=".$panel->round->timeslot->id."&judge_id=$judge_id&err=$err") if $err;
		$m->redirect("../index.mhtml?all=$all&timeslot_id=".$panel->round->timeslot->id."&msg=$msg");

	} else { 

		foreach my $ballot (@ballots) {

			foreach my $student ($ballot->entry->students) { 
				$speaker_points{$student->id} += $ARGS{"points_".$student->id};
				$team_points{$ballot->entry->id} += $ARGS{"points_".$student->id};
				$ballot_err .= "Points ".$ARGS{"points_".$student->id}." are outside range" if $ARGS{"points_".$student->id} < $min || $ARGS{"points_".$student->id} > $max;
			}

			$ballot_err .= " Duplicated team points of ".$team_points{$ballot->entry->id} if $already{$team_points{$ballot->entry->id}};
			$already{$team_points{$ballot->entry->id}}++;
		}

		$m->redirect("../index.mhtml?all=$all&timeslot_id=".$panel->round->timeslot->id."&judge_id=$judge_id&ballot_err=$ballot_err&err=$err") if $ballot_err;

		my %done_team;
		my %done_student;

		@ballots = sort {$team_points{$b->entry->id} <=> $team_points{$a->entry->id}} @ballots;

		my $rank;

		foreach my $ballot (@ballots) {

			$rank++;
			$team_rank{$ballot->entry->id} = $rank;

			foreach my $value (@{$values_by_ballot{$ballot->id}}) { 

				if ($value->tag eq "points") { 

					if ($value->value != $speaker_points{$value->student}) { 
						$ballot_err = "Points disagreed.  Please enter again";
						$value->value($speaker_points{$value->student});
						$value->update;
					}

					$done_student{$value->student}++;
				}

				if ($value->tag eq "rank") { 

					if ($value->value != $rank) { 
						$ballot_err = "Ranks disagreed.  Please enter again";
						$value->value($rank);
						$value->update;
					}

					$done_team{$ballot->entry->id}++;

				}

			}


		}

		my $undone;

		foreach my $ballot (@ballots) {

			foreach my $student ($ballot->entry->students) { 

				next if $done_student{$student->id};
				$undone++;

				Tab::Score->create({
					ballot => $ballot->id,
					tag => "points",
					student => $student->id,
					value => $speaker_points{$student->id}
				});
				
			}

			next if $done_team{$ballot->entry->id};
			$undone++;

			Tab::Score->create({
				ballot => $ballot->id,
				tag => "rank",
				value => $team_rank{$ballot->entry->id}
			});

		}

		$m->redirect("../index.mhtml?all=$all&timeslot_id=".$panel->round->timeslot->id."&judge_id=$judge_id&ballot_err=$ballot_err&err=$err") if $ballot_err;

		unless ($undone) {

			foreach my $ballot ($panel->ballots) { 
				$ballot->audit(1);
				$ballot->entered_by($person->id);
				$ballot->update;
			}

			$msg = "Double check confirmed.  Ballot complete.";

		} else { 

			foreach my $ballot (@ballots) { 
				$ballot->entered_by($person->id);
				$ballot->update;
			}

			$msg = "First entry complete.  Re-enter ballot to confirm";
		}


		$rank_string = '<h3>Ballot first entry done.  Hand it to second entry</h3>';
		$rank_string = '<h3>Ballot confirmed</h3>' unless $undone;

		foreach my $ballot (sort {$a->speakerorder <=> $b->speakerorder} @ballots) {
			$rank_string .= '<div class="padmore evenrow block martop"><span class="medspan"><h4>'.$team_rank{$ballot->entry->id}.'</h4></span><span class="medbigspan">'.$ballot->entry->code.'</span>
			<span class="medspan">'.$team_points{$ballot->entry->id}." total points</span></div>";
		}

	}


	$m->redirect("../index.mhtml?all=$all&timeslot_id=".$panel->round->timeslot->id."&err=$err&rank_string=$rank_string&msg=$msg");

</%init>

