<%args>
	$tourn
	$tourn_settings
	$person
</%args>
<%init>

	use Text::CSV;

	my $tz = $tourn->tz;
	$tz = "UTC" unless $tz;

	$m->print("<div id=\"wrapper\">");
	$m->print("<div id=\"content\">");

	$m->print("<div class=\"main\">");
	$m->print("<h2>Uploading data</h2>");

	$m->print("<span class='nospace upload_scroll'>");
	$m->flush_buffer();

	my $nats = $tourn->setting("nsda_nats");
	my $now = DateTime->now( time_zone => $tz);

	# Get the upload and create the file handle.
	my $req = Apache2::Request->new($r);
	my @csv_handles = $r->upload();
	my $csv_file = $req->upload($csv_handles[0]);
	my $io = $csv_file->io;

	my $csv = Text::CSV->new({ sep_char => ',' });

	my $skipped;
	my $counter;

	my $dbh = Tab::DBI->db_Main();
	my $sth = $dbh->prepare("
		select judge.id from judge, category, person where person.email = ?
			and person.id = judge.person
			and judge.category = category.id
			and category.tourn = ?
	");

	my $entry_sth = $dbh->prepare("
		select entry.id from entry, event
			where entry.code = ?
			and entry.event = event.id
			and event.tourn = ?
	");

	LINE:
	foreach my $line (<$io>) {

		unless ($csv->parse($line)) {
			Tab::debuglog("Line $line failed to parse");
			next LINE;
		}

		my ($judge_email, @entry_codes) = $csv->fields();

		$judge_email =~ s/^"//;
		$judge_email =~ s/"$//g;
		$judge_email =~ s/^'//;
		$judge_email =~ s/'$//g;

		$sth->execute(lc($judge_email), $tourn);
		my $results = $sth->fetchall_hash();

		if ($results && @{$results}) {

			my $judge = shift @{$results};

			$m->print("<p>Conflicts for $judge_email:</p>");

			foreach my $code (@entry_codes) {
				$code =~ s/^"//;
				$code =~ s/"$//g;
				$code =~ s/^'//;
				$code =~ s/'$//g;

				$entry_sth->execute($code, $tourn);
				my $entries = $entry_sth->fetchall_hash();

				if ($entries && @{$entries}) {

					my $entry = shift @{$entries};

					Tab::Strike->create({
						registrant => 1,
						type       => "entry",
						conflict   => 1,
						judge      => $judge->{id},
						entry      => $entry->{id},
						tourn      => $tourn->id
					});

				} else {
					$m->print("<p class='padleft'>No entry found for $code</p>");
				}
			}

		} else {
			$m->print("<p>No judge record for $judge_email:</p>");
		}

		$m->flush_buffer();
	}

	$m->print("</span>");

	$m->print("<h5 class='centeralign redtext'>Data Upload Complete!</h5>");
	$m->print("</div>");
	$m->print("</div>");
	$m->print("</div>");
	$m->flush_buffer();
	$m->abort();

</%init>
