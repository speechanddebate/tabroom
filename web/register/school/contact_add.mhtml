<%args>
	$person
	$school_id
	$contact_id    => undef
	$contact_email => undef
</%args>
<%init>

	my $msg;

	my $school = Tab::School->retrieve($school_id);

	unless ($school) {
		$m->comp("/funclib/abort.mas",
			message => "No valid Tabroom school found with ID $school_id"
		);
	}

	my $contact;

	if ($contact_id) {
		$contact = Tab::Person->retrieve($contact_id);
	} elsif ($contact_email) {
		$contact = Tab::Person->search( email => $contact_email )->first;
	}

	unless ($contact) {
		$m->comp("/funclib/abort.mas",
			message => "No valid Tabroom account found with ID $contact_id or email $contact_email"
		);
	}

	my @already = Tab::Contact->search(
		school => $school,
		person => $contact->id
	);

	if (@already) {

		$msg .= $contact->email." is already a contact";

	} else {

		my $nsda;

		if ($contact->nsda > 0) {

			my ($roster_ref, $raw) = $m->comp(
				"/funclib/nsda/school_roster.mas",
				chapter => $school->chapter
			);

			my %coaches;

			if ($raw) {
				%coaches = map { $_->{person_id} => $_->{role} } @{$roster_ref};
			}

			if (
				$coaches{$contact->nsda} eq "Advisor"
				|| $coaches{$contact->nsda} eq "Coach"
			) {
				$nsda = $contact->nsda;
			}
		}

		Tab::Contact->create({
			school     => $school,
			person     => $contact->id,
			nsda       => $nsda,
			created_by => $person,
			book       => 1,
			email      => 1,
			official   => 1,
			onsite     => 1,
		});

		$msg .= "Added ".$contact->email." to your contacts list";
	}

	$m->redirect("edit.mhtml?school_id=".$school->id."&msg=$msg");

</%init>
