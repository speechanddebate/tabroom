<%args>
	$tourn
	$tourn_settings
	$person
	$entry_id     => undef
	$name         => undef
	$code         => undef
	$ada          => undef
	$seed         => undef
	$pairing_seed => undef
	$atlarge      => undef
	$pod          => undef
</%args>
<%init>

	my $entry = Tab::Entry->retrieve(int($entry_id));

	unless ($entry) {
		$m->comp("/funclib/abort.mas", message => "No entry found for ID $entry_id");
	}

	my $school = $entry->school;
	my $now = DateTime->now();

	unless ($name) {
		$name = $m->comp("/funclib/entry_name.mas", entry => $entry);
	}

	if ($name && $name ne $entry->name) {
		$entry->name($name);
		$entry->update;
	}

	my $event = $entry->event;
	my %event_settings = $event->all_settings;

	$code =~ s/[^\w\s&-\/]//g;

	my $msg;
	my $err;

	if ($code ne $entry->code) {

		my @existing;

		if ($code && (not defined $tourn_settings->{"nonunique_entry_codes"}))  {

			Tab::Entry->set_sql( by_code => "
				select distinct entry.*
					from entry, event
				where entry.event = event.id
					and event.tourn = ?
					and entry.code = ?
					and entry.unconfirmed = 0
				order by entry.name
			");

			push @existing, Tab::Entry->search_by_code($tourn->id, $code);

			Tab::Judge->set_sql( by_code => "
				select distinct judge.*
					from judge, category
				where judge.category = category.id
					and category.tourn = ?
					and judge.code = ?
				order by judge.last
			");

			push @existing, Tab::Judge->search_by_code($tourn->id, $code);
		}

		if (@existing) {
			$err = "Warning: ".scalar @existing." entries or judges with code $code already exist.";
		}

		$msg .= " Entry code ".$entry->code." changed to ".$code;
		$entry->code($code);

		my $change = $m->comp("/funclib/log.mas",
			tourn       => $tourn->id,
			entry       => $entry,
			event       => $event,
			type        => "tabbing",
			description => $msg,
			person      => $person
		);
	}

	$entry->ada($ada);
	$entry->update;

	$entry->setting("backtab", $ARGS{"backtab"});
	$entry->setting("registered_seed", $seed);
	$entry->setting("atlarge", $atlarge);
	$entry->setting("observers", $ARGS{"observers"});

	if ($tourn_settings->{"nsda_nats"}) {
		if ($person->site_admin) {
			$entry->setting("source_entry", $ARGS{source_entry});
		}
	}

    if ($seed eq "full") {
        $entry->setting("pairing_seed", 1);
    } elsif ($seed eq "half") {
        $entry->setting("pairing_seed", 2);
    } elsif ($seed eq "free") {
        $entry->setting("pairing_seed", 3);
    } elsif ($event_settings{"apda"}) {
        $entry->setting("pairing_seed", 4);
    } else {
		$entry->setting("pairing_seed", $pairing_seed);
    }

	$entry->setting("pod", $pod);

	if ($tourn_settings->{"nsda_district"}
		|| $tourn_settings->{"nsda_ms_nats"}
		|| $tourn_settings->{"nsda_nats"}
	) {

		$entry->setting("nsda_house_bloc", $ARGS{"nsda_house_bloc"});
	}

	if ($tourn_settings->{"nsda_nats"}) {

		$entry->setting("coach_points", $ARGS{"coach_points"} || 0);

		if ($ARGS{'coach_points'} > 0) {
			my $coach_person = Tab::Person->search(nsda => $ARGS{"coach_points"})->first;

			unless ($coach_person) {
				$coach_person = $m->comp(
					"/funclib/nsda/user_import.mas",
					nsda_id => $ARGS{"coach_points"}
				);
			}
		} elsif ($ARGS{"coach_points_id"}) {

			my $coach_person = Tab::Person->search(nsda => $ARGS{"coach_points_id"})->first;

			unless ($coach_person) {
				$coach_person = $m->comp(
					"/funclib/nsda/user_import.mas",
					nsda_id => $ARGS{"coach_points_id"}
				);
			}

			if ($coach_person) {
				$entry->setting("coach_points", $ARGS{"coach_points_id"});
			}
		}
	}

	if ($tourn_settings->{"nsda_nats"} || $tourn_settings->{"nsda_ms_nats"} ) {
		$entry->setting("coach_script", $ARGS{"coach_script"});
	}

	if ($event_settings{"breakouts"}) {

		foreach my $breakout (1 .. $event_settings{"breakouts"}) {
			next if $event_settings{"breakout_".$breakout."_delete"};

			$entry->setting("breakout_".$breakout, $ARGS{"breakout_".$breakout})
				if $ARGS{"breakout_".$breakout};

			$entry->setting("breakout_".$breakout, 0)
				unless $ARGS{"breakout_".$breakout};
		}
	}

	$msg = "Entry information updated";

	if ($tourn_settings->{'nsda_nats'}) {

		if ($event_settings{"supp"} || $event_settings{"conn"}) {

			$entry->setting("reregistered", $ARGS{"reregistered"});

			if ($ARGS{"reregistered"}) {
				$entry->unconfirmed(0);
				$entry->dropped(0);
				$entry->update();
			}

		} else {

			if ($ARGS{"entry_status"} eq "accepted") {

				$entry->unconfirmed(0);
				$entry->update();

				$entry->setting('accepted_by', $person->id);
				$entry->setting('accepted_at', "date", $now);

				$entry->setting('rejected_by', 0);
				$entry->setting('rejected_at', 0);

				$msg .= ".  Entry marked accepted";

			} elsif ($ARGS{"entry_status"} eq "rejected") {

				$entry->unconfirmed(1);
				$entry->update();

				$entry->setting('accepted_by', 0);
				$entry->setting('accepted_at', 0);

				$entry->setting('rejected_by', $person->id);
				$entry->setting('rejected_at', "date", $now);

				$msg .= ".  Entry marked rejected. <br /> Promotion process said: ";

		        my $return = $m->comp(
		            "/funclib/promote_slot.mas",
			            entry  => $entry,
			            person => $person
		        );

				$msg .= $return;

			} elsif ($ARGS{"entry_status"} eq "rejected_no_promotion") {

				$entry->unconfirmed(1);
				$entry->update();

				$entry->setting('accepted_by', 0);
				$entry->setting('accepted_at', 0);
				$entry->setting('rejected_by', $person->id);
				$entry->setting('rejected_at', "date", $now);

				$msg .= ".  Entry marked rejected and next alternate was NOT promoted";

			} elsif ($ARGS{"entry_status"} eq "pending") {

				$entry->unconfirmed(1);
				$entry->update();

				$entry->setting('accepted_by', 0);
				$entry->setting('accepted_at', 0);
				$entry->setting('rejected_by', 0);
				$entry->setting('rejected_at', 0);

				$msg .= ".  Entry marked pending ";
			}


			if (
				$ARGS{"entry_status"} eq "rejected_no_promotion"
				|| $ARGS{"entry_status"} eq "rejected"
			) {

				my $tz = $tourn->tz;
				$tz = "UTC" unless $tz;

				my $fifty_percent_deadline = $tourn_settings->{"fifty_percent_deadline"};
				$fifty_percent_deadline->set_time_zone($tz) if $fifty_percent_deadline;

				my $hundred_percent_deadline = $tourn_settings->{"hundred_percent_deadline"};
				$hundred_percent_deadline->set_time_zone($tz) if $hundred_percent_deadline;

				my $now = DateTime->now(
					time_zone => $tz
				);

				my $created = $entry->created_at;
				$created->add(days => 7);

				my $fee = $event->fee;

				if ($tourn->hotels()) {

					my $hotel = eval {
						return Tab::Hotel->retrieve($school->setting("hotel"));
					};

					if ($hotel) {
						if ($hotel->surcharge) {
							foreach my $student ($entry->students) {
								$fee = $fee += $hotel->surcharge;
							}
						} elsif ($hotel->multiple) {
							$fee = $fee * $hotel->multiple;
						}
					}
				}

				if ($created > $now) {

					# No fee for recently promoted alternates being dropped

				} elsif ( $hundred_percent_deadline && $hundred_percent_deadline < $now) {

					my $drop_reason = "Dropped entry ".$entry->name." in ".$event->abbr." after the ".$hundred_percent_deadline->month."/".$hundred_percent_deadline->day." deadline";

					my $fine = Tab::Fine->create({
						school    => $school->id,
						amount    => $fee,
						reason    => $drop_reason,
						levied_at => $now,
						levied_by => $person->id
					});

					$msg .= " and the full late drop fine was assessed ";

				} elsif ( $fifty_percent_deadline && $fifty_percent_deadline < $now) {

					my $drop_reason = "Dropped entry ".$entry->name." in ".$event->abbr." after the ".$fifty_percent_deadline->month."/".$fifty_percent_deadline->day." deadline";

					my $fine = Tab::Fine->create({
						school    => $school->id,
						amount    => ($fee / 2),
						reason    => $drop_reason,
						levied_at => $now,
						levied_by => $person->id
					});

					$msg .= " and a 50% late drop fine was assessed ";

				}
			}
		}

		foreach my $student ($entry->students) {
			$m->comp("/funclib/nsda/status_check.mas",
				school  => $school,
				student => $student
			);
		}
	}

	$m->redirect("edit.mhtml?entry_id=$entry_id&err=$err&msg=$msg");

</%init>
