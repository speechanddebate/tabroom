<%args>
	$tourn
	$round_id => undef
</%args>
<%init>

	my $round = Tab::Round->retrieve($round_id);

	my @entries = $round->event->entries;

	my %judges_by_panel = ();

	foreach my $panel ($round->panels) { 
		push @{$judges_by_panel{$panel->id}}, $m->comp("/funclib/panel_judges.mas", panel => $panel);
	}

	ENTRY:
	foreach my $entry (@entries) { 

		my $panel_id = $ARGS{$entry->id};


		my @ballots = $m->comp("/funclib/entry_ballots.mas", entry => $entry, round => $round);

		my $ok;

		foreach my $ballot (@ballots) { 

			if ($panel_id && $ballot->panel->id == $panel_id) { 
				$ok++;
			} else { 
				$ballot->delete;
			}

		}

		next ENTRY if $ok;
		next ENTRY unless $panel_id;


		if ($judges_by_panel{$panel_id} && @{$judges_by_panel{$panel_id}}) { 

			foreach my $judge (@{$judges_by_panel{$panel_id}}) { 

				Tab::Ballot->create({
					panel => $panel_id,
					judge => $judge->id,
					entry => $entry->id
				});

			}
		
		} else { 

			Tab::Ballot->create({
				panel => $panel_id,
				entry => $entry->id
			});

		}

	}

	my $msg = "Manual panels saved";

	$m->redirect("/panel/schemat/show.mhtml?round_id=".$round->id."&msg=$msg");

</%init>
