<%args>
	$school_id
</%args>
<%perl>
	
	my $school = Tab::School->retrieve($school_id);
	my $tourn = $school->tourn;
	my $now = DateTime->now;

	my $err;

	CONCESSION:
	foreach my $concession ($tourn->concessions) { 
	
		my $total_order = $m->comp("/funclib/concession_stats.mas", total => 1, concession => $concession);
		my $cap = $concession->cap;
		my $quantity = $ARGS{$concession->id."_amount"};

		my $purchase = Tab::ConcessionPurchase->search( concession => $concession->id, school => $school->id )->first;

		next if ($purchase && $purchase->quantity == $quantity);

		$quantity = $quantity - $purchase->quantity  if $purchase;

		if ($quantity < 0 ) { 
			$purchase->quantity($purchase->quantity + $quantity) if $purchase;
			$purchase->update;
			next CONCESSION;
		}

		if ( ($total_order + $quantity) > $cap) { 

			my $remainder = $cap - $total_order;

			Tab::debuglog("Remainder is $remainder, cap is $cap, total is $total_order, new is $quantity");

			if ($remainder > 0 ) { 
				$quantity = $remainder; 
				$err .= "You hit the limit of availability for ".$concession->name."  Your order has been reduced to $quantity <br />";
			} else { 
				$err .= "No ".$concession->name." remain to be ordered. <br />";
				$quantity = 0;
			}

		}

		if ($purchase) { 

			$purchase->quantity( $purchase->quantity + $quantity );
			$purchase->update;

		} else {

			$purchase = Tab::ConcessionPurchase->create({ 
				concession => $concession->id,
				school => $school->id,
				quantity => $quantity,
				placed => $now
			});
		
		}

	}

	my $msg = "Order updated";

	$m->redirect("concessions.mhtml?school_id=$school_id&msg=$msg&err=$err");

</%perl>

