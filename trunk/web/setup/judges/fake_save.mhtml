<%args>
	$group_id
	$num_schools
	$num_hires
</%args>
<%init>

	use POSIX;
	use List::Util 'shuffle';

	my $total = $num_schools + $num_hires;

	my $group = Tab::JudgeGroup->retrieve($group_id);

	my @schools = $m->comp("/funclib/group_schools.mas", group => $group);

	my %school_entries;
	my $total_entries;

	Tab::Judge->set_sql( delete_ballots => "
		delete ballot.* from ballot, judge
		where judge.judge_group = ? 
		and judge.id = ballot.judge
	");

	Tab::Judge->set_sql( delete_prefs => "
		delete rating.* from 
		rating, judge
		where judge.judge_group = ? 
		and judge.id = rating.judge
	");

	Tab::Judge->set_sql( delete_judges => "
		delete from judge where judge_group = ? 
	");

	Tab::Judge->sql_delete_ballots->execute($group->id);
	Tab::Judge->sql_delete_prefs->execute($group->id);
	Tab::Judge->sql_delete_judges->execute($group->id);

	my @all_entries;

	foreach my $school (@schools) { 
		my @entries = $m->comp("/funclib/group_entries.mas", group => $group, school => $school);
		@{$school_entries{$school->id}} = @entries;
		$total_entries += scalar @entries;
		push @all_entries, @entries;
	}

	my %school_quota;

	foreach my $school (@schools) { 
		my $percentage = ((scalar @{$school_entries{$school->id}}) / $total_entries) ;
		$school_quota{$school->id} = ceil($percentage * $num_schools);
	}

	my $counter;

	foreach my $school (@schools) { 

		foreach (1 .. $school_quota{$school->id}) { 

			next if $counter >= $total;
			$counter++;

			Tab::Judge->create({
				judge_group => $group->id,
				school      => $school->id,
				first       => "Test",
				last        => $counter,
				code        => $counter + 999,
				active      => 1,
				obligation  => (rand(7) + 1),
				tab_rating  => (rand(99) + 1)
			});

		}
	}

	foreach (1 .. $num_hires) { 

		next if $counter >= $total;
		$counter++;

		Tab::Judge->create({
			judge_group => $group->id,
			school      => 0,
			first       => "Hired",
			last        => $counter,
			code        => $counter + 999,
			active      => 1,
			hired       => (rand(7) + 1),
			tab_rating  => (rand(99) + 1)
		});

	}

	if ($group->setting("prefs")) { 

		my @judges = $group->judges;
		my $num_judges = scalar @judges;

		if ($group->setting("prefs") eq "ordinals") { 

			foreach my $entry (@all_entries) { 

				@judges = shuffle(@judges);

				my $count;

				foreach my $judge (@judges) { 

					$count++;
					my $percentile = $count / $num_judges;

					Tab::Rating->create({ 
						entry      => $entry->id,
						judge      => $judge->id,
						type       => "entry",
						ordinal    => $count,
						percentile => $percentile
					});
				}

			}

		} else { 

			my @tiers = $group->rating_tiers;

			foreach my $entry (@all_entries) { 

				@judges = shuffle @judges;

				foreach my $judge (@judges) { 

					@tiers = shuffle @tiers;

					my $tier_id = $tiers[0]->id;

					Tab::Rating->create({ 
						entry       => $entry->id,
						judge       => $judge->id,
						type        => "entry",
						rating_tier => $tier_id
					});
				}

			}
		}
	}


	my $msg = "Created $counter fake judges in ".$group->abbr;

	$m->redirect("fake.mhtml?group_id=$group_id&msg=$msg");

</%init>

