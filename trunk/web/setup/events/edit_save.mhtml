<%args>
	$tourn
	$event_id => undef
	$name
	$abbr           => undef
	$fee            => undef
	$min_entry      => undef
	$max_entry      => undef
	$judge_group_id => undef
	$code_style     => undef
	$type           => undef
	$level          => undef
	$clone_event    => undef
</%args>
<%init>

	my $event;
	my $msg;

	if ($event_id) { 

		$event = Tab::Event->retrieve($event_id);

		$event->name($name);
		$event->type($type);
		$event->abbr($abbr);
		$event->judge_group($judge_group_id);
		$event->fee($fee);
		$event->update;

		$msg = "Event changes saved";

	} else { 

		my $code_start;

		my @code_events = Tab::Event->search( tourn => $tourn->id );
		@code_events = sort {$a->setting("code_start") <=> $b->setting("code_start")} @code_events;

		if (@code_events) { 
			my $last_code_event = pop(@code_events); 		
			my $last_code = $last_code_event->setting("code_start"); 
			$code_start = $last_code; 
			$code_start = $code_start + 100;
		} else {
			$code_start = 100; 
		}
	
		$event = Tab::Event->create({
			name        => $name,
			type        => $type,
			abbr        => $abbr,
			tourn       => $tourn->id,
			judge_group => $judge_group_id,
			fee         => $fee
		});
	
		$event->setting("code_start", $code_start);
		$msg ="Event $name created";

	}

	$event->setting("level", $level);
	$event->setting("min_entry", $min_entry);
	$event->setting("max_entry", $max_entry);
	$event->setting("code_style", $code_style);

	if ($clone_event) { 

		my @settings = Tab::EventSetting->search( event => $clone_event );
		my %already_settings = map {$_->tag => $_} Tab::EventSetting->search( event => $event->id );

		foreach my $setting (@settings) { 
			next if $setting->tag eq "level" && $level;
			next if $setting->tag eq "min_entry" && $min_entry;
			next if $setting->tag eq "max_entry" && $max_entry;
			next if $setting->tag eq "code_start";

			if ($already_settings{$setting->tag}) { 
				$already_settings{$setting->tag}->value($setting->value);
				$already_settings{$setting->tag}->update;
			} else { 
				my $stolen = $setting->copy;
				$stolen->event($event->id);
				$stolen->update;
			}
		}

		my $ce = Tab::Event->retrieve($clone_event);

		$msg .= "<br />Settings from ".$ce->name." cloned";

	}

	$m->redirect("edit.mhtml?event_id=".$event->id."&msg=$msg");
		
</%init>
