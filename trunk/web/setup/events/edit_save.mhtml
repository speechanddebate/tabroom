<%args>
	$tourn
	$event_id             => undef
	$name                 => undef
	$abbr                 => undef
	$fee                  => undef
	$min_entry            => undef
	$max_entry            => undef
	$judge_group_id       => undef
	$code_style           => undef
	$code_start           => undef
	$type                 => undef
	$level                => undef
	$nsda_debate_category => undef
	$nsda_speech_category => undef
	$clone_event          => undef
</%args>
<%init>

	my $event;
	my $msg;

	if ($event_id) { 

		$event = Tab::Event->retrieve($event_id);

		$event->name($name);
		$event->type($type);
		$event->abbr($abbr);
		$event->judge_group($judge_group_id);
		$event->fee($fee);
		$event->update;

		$msg = "Event changes saved";

	} elsif ($name) { 

		my @code_events = Tab::Event->search( tourn => $tourn->id );
		@code_events = sort {$a->setting("code_start") <=> $b->setting("code_start")} @code_events;

		if (@code_events) { 
			my $last_code_event = pop(@code_events); 		
			my $last_code = $last_code_event->setting("code_start"); 
			$code_start = $last_code; 
			$code_start = $code_start + 100;
		} else {
			$code_start = 100; 
		}
	
		$event = Tab::Event->create({
			name        => $name,
			type        => $type,
			abbr        => $abbr,
			tourn       => $tourn->id,
			judge_group => $judge_group_id,
			fee         => $fee
		});
		
		$msg ="Event $name created";
	
	}

	$m->abort unless $event;

	$event->setting("code_start", $code_start);
	$event->setting("level", $level);
	$event->setting("min_entry", $min_entry);
	$event->setting("max_entry", $max_entry);
	$event->setting("code_style", $code_style);

	my $nsda_cat = 0;
	$nsda_cat = $nsda_speech_category if $nsda_speech_category;
	$nsda_cat = $nsda_debate_category if $nsda_debate_category;
	$event->setting("nsda_event_category", $nsda_cat);
	$msg .= " Category is $nsda_cat";

	if ($clone_event) { 

		my @settings = Tab::EventSetting->search( event => $clone_event );
		my %already_settings = map {$_->tag => $_} Tab::EventSetting->search( event => $event->id );

		foreach my $setting (@settings) { 
			next if $setting->tag eq "level" && $level;
			next if $setting->tag eq "nsda_event_category" && $nsda_cat;
			next if $setting->tag eq "min_entry" && $min_entry;
			next if $setting->tag eq "max_entry" && $max_entry;
			next if $setting->tag eq "code_start";

			if ($already_settings{$setting->tag}) { 
				$already_settings{$setting->tag}->value($setting->value);
				$already_settings{$setting->tag}->update;
			} else { 
				my $stolen = $setting->copy({
					event => $event->id
				});
			}
		}

		my $ce = Tab::Event->retrieve($clone_event);

		$msg .= "<br />Settings from ".$ce->name." cloned";

	}

	unless ($event->setting("aff_label")) { 
		$event->setting("aff_label", "Aff");
		$event->setting("aff_label", "Pro") if $type eq "pf";
		$event->setting("aff_label", "Gov") if $type eq "parli";
	}

	unless ($event->setting("neg_label")) { 
		$event->setting("neg_label", "Neg");
		$event->setting("neg_label", "Con") if $type eq "pf";
		$event->setting("neg_label", "Opp") if $type eq "parli";
	}

	$m->redirect("edit.mhtml?event_id=".$event->id."&msg=$msg");
		
</%init>
